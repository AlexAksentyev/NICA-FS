INCLUDE 'bin/support/support';


PROCEDURE BETAFUNC;
  VARIABLE RESTMASSAMU 1; VARIABLE GAMMA 1; VARIABLE EKIN 1;
  VARIABLE WHERE 100;
  {lattice parameters}
  VARIABLE QF  1; VARIABLE QD  1;
  VARIABLE HZ1 1; VARIABLE HZ2 1;
  VARIABLE KZ1 1; VARIABLE KZ2 1;
  VARIABLE MAPPARAMS 1 6; {HOLDS THE ABOVE VALUES}
  VARIABLE MAPARR 1000 6 1280; VARIABLE SPNRARR 1000 3 3 1280; {element map arrays}
  {variables required by GT}
  VARIABLE F 100 6; VARIABLE A 100 2; VARIABLE B 100 2;
  VARIABLE G 100 2; VARIABLE R 100 2; VARIABLE MU 100 2; 

  VARIABLE EL 1; {index of the current starting element}
  VARIABLE EL0 1; VARIABLE EL1 1; {select lattice segment from EL0 to EL1 for test}
  VARIABLE PCL 2 2; VARIABLE PID 1; {select which particle (proton, deuteron) to inject}
  
  PROCEDURE PICK INDEX;
    VARIABLE I 1; VARIABLE J 1;
    LOOP I 1 TWOND; MSC(I) := MAPARR(I, INDEX); ENDLOOP;
    LOOP I 1 3; LOOP J 1 3; SSCR(I,J) := SPNRARR(I,J,INDEX) + 0*DD(1);
    ENDLOOP;ENDLOOP;
  ENDPROCEDURE;
  PROCEDURE MAKEMAP FROM TO START; {lattice segment FROM index TO index, beginning at START index}
    VARIABLE J 1;
    WRITE 6 '* '&ST(START);
    RMAPS START MAPARR SPNRARR; {sets the MAP, SPNR transfer maps to those of the i-th element}
    LOOP J (START+1) TO; WRITE 6 '**       '&ST(J);
      PICK J; LOCSET 0 0 0 0 0 0; UPDATE 1 1;
      ENDLOOP;
    LOOP J FROM (START-1); WRITE 6 '**       '&ST(J);
      PICK J; LOCSET 0 0 0 0 0 0; UPDATE 1 1;
      ENDLOOP;
  ENDPROCEDURE;

  DIRSET WHERE 'data/BETA-FUNCTION/' TRUE;
  GROUTF 'img/dump/TR' 1;
  PCL(1) := 1.00728&1.792847; {proton rest mass and G}
  PCL(2) := 2.01410&(-.142987); {deuteron rest mass and G}
  
  OV 1 2 0; PID := 1;
  RESTMASSAMU := PCL(PID)|1;
  GAMMA := 1.14;
  EKIN := (GAMMA-1)*RESTMASSAMU*AMUMEV;
  WRITE 6 'EKIN = '&ST(EKIN);
  RP EKIN RESTMASSAMU 1;
  RPS 1 PCL(PID)|2;

  {LATTICE PARAMETERS:}
  { IF M0<2; SET_FOR_PROTONS HZ1 HZ2 QF QD; WRITE 6 'SETUP FOR PROTONS'; }
  { ELSEIF TRUE; SET_FOR_DEUTERONS HZ1 HZ2 QF QD; WRITE 6 'SETUP FOR DEUTERONS'; }
  { ENDIF; }

  MAPPARAMS(1) := HZ1; MAPPARAMS(2) := HZ2;
  MAPPARAMS(3) := KZ1; MAPPARAMS(4) := KZ2;
  MAPPARAMS(5) := QF;  MAPPARAMS(6) := QD;

  UM;
  BENDS24_SETUP HZ1 HZ2 KZ1 KZ2 QF QD 0;
  { FULL_SETUP; }
  OPENF 935 WHERE&'MAP0' 'REPLACE'; PM 935; CLOSEF 935;
  GT MAP F MU A B G R;
  WRITE 6 'FROM GT';
  WRITE 6 'BETA' B(1) B(2);
  WRITE 6 'MU' MU(1) MU(2);
  WRITE 6 'R' R(1) R(2);


  MAPSEQ24BENDS MAPPARAMS MAPARR SPNRARR; {computes element matrices}
  { MAPSEQ_NICA_FULL MAPARR SPNRARR; }

  EL0 := 7; EL1 := 18;
  OPENF 3617 WHERE&'BETA.dat' 'REPLACE';
  OPENF 3618 WHERE&'MU.dat' 'REPLACE';
  WRITE 3617 '# EL BX-RE BX-IM BY-RE BY-IM';
  LOOP EL EL0 EL1;
    MAKEMAP EL0 EL1 EL; OPENF 935 WHERE&'MAP'&MRK(EL) 'REPLACE'; PM 935; CLOSEF 935;
    GT MAP F MU A B G R;
    B(1) := CM(CONS(B(1))); B(2) := CM(CONS(B(2)));
    MU(1) := CM(CONS(MU(1))); MU(2) := CM(CONS(MU(2)));
    WRITE 3617 SF(EL,'(I6)')&SF(B(1)|1,'(E15.7)')&SF(B(1)|2,'(E15.7)')&SF(B(2)|1,'(E15.7)')&SF(B(2)|2,'(E15.7)');
    WRITE 3618 SF(EL,'(I6)')&SF(MU(1)|1,'(E15.7)')&SF(MU(1)|2,'(E15.7)')&SF(MU(2)|1,'(E15.7)')&SF(MU(2)|2,'(E15.7)');
  ENDLOOP;
  CLOSEF 3617; CLOSEF 3618;

ENDPROCEDURE; {BETAFUNC}

PROCEDURE RUN;
  BETAFUNC;
ENDPROCEDURE;
RUN; END;
