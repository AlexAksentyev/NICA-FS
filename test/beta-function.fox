INCLUDE 'bin/support/support';


PROCEDURE RUN;
  VARIABLE RESTMASSAMU 1; VARIABLE GAMMA 1; VARIABLE EKIN 1;
  VARIABLE WHERE 100;
  VARIABLE QF  1; VARIABLE QD  1;
  VARIABLE HZ1 1; VARIABLE HZ2 1;
  VARIABLE KZ1 1; VARIABLE KZ2 1;
  VARIABLE MAPPARAMS 1 6; {HOLDS THE ABOVE VALUES}
  VARIABLE MAPARR 1000 6 1280; VARIABLE SPNRARR 1000 3 3 1280;
  VARIABLE F 100 6; VARIABLE A 100 2; VARIABLE B 100 2;
  VARIABLE G 100 2; VARIABLE R 100 2; VARIABLE MU 100 2; 

  VARIABLE I 1; VARIABLE NEL 1; {select the 1st NEL elements from the lattice to compute the beta functions}
  
  PROCEDURE PICK INDEX;
    VARIABLE I 1; VARIABLE J 1;
    LOOP I 1 TWOND; MSC(I) := MAPARR(I, INDEX); ENDLOOP;
    LOOP I 1 3; LOOP J 1 3; SSCR(I,J) := SPNRARR(I,J,INDEX) + 0*DD(1);
    ENDLOOP;ENDLOOP;
  ENDPROCEDURE;
  PROCEDURE MAKEMAP INDEX;
    VARIABLE J 1;
    WRITE 6 '* '&ST(INDEX);
    RMAPS INDEX MAPARR SPNRARR; {sets the MAP, SPNR transfer maps to those of the i-th element}
    LOOP J (INDEX+1) NEL; WRITE 6 '**       '&ST(J);
      PICK J; LOCSET 0 0 0 0 0 0; UPDATE 1 1;
      ENDLOOP;
    LOOP J 1 (INDEX-1); WRITE 6 '**       '&ST(J);
      PICK J; LOCSET 0 0 0 0 0 0; UPDATE 1 1;
      ENDLOOP;
  ENDPROCEDURE;

  DIRSET WHERE 'data/BETA-FUNCTION/' TRUE;
  GROUTF 'img/dump/TR' 1;
  
  OV 1 2 0;
  RESTMASSAMU := 1.00728; {deuteron 2.01410}
  GAMMA := 1.14;
  EKIN := (GAMMA-1)*RESTMASSAMU*AMUMEV;
  WRITE 6 'EKIN = '&ST(EKIN);
  RP EKIN RESTMASSAMU 1; {inject protons}
  RPS 1 1.792847; {deuteron G = -.142987}

  {LATTICE PARAMETERS:}
  IF M0<2; SET_FOR_PROTONS HZ1 HZ2 QF QD; WRITE 6 'SETUP FOR PROTONS';
  ELSEIF TRUE; SET_FOR_DEUTERONS HZ1 HZ2 QF QD; WRITE 6 'SETUP FOR DEUTERONS';
  ENDIF;

  MAPPARAMS(1) := HZ1; MAPPARAMS(2) := HZ2;
  MAPPARAMS(3) := KZ1; MAPPARAMS(4) := KZ2;
  MAPPARAMS(5) := QF;  MAPPARAMS(6) := QD;

  UM; NEL := 1279;
  { BENDS24_SETUP HZ1 HZ2 KZ1 KZ2 QF QD 0; }
  FULL_SETUP;
  OPENF 935 WHERE&'MAP0' 'REPLACE'; PM 935; CLOSEF 935;
  GT MAP F MU A B G R;
  WRITE 6 CONS(B(1)) CONS(B(2));

  { MAPSEQ24BENDS MAPPARAMS MAPARR SPNRARR; } {computes element matrices}
  MAPSEQ_NICA_FULL MAPARR SPNRARR;

  OPENF 3617 WHERE&'BETA.dat' 'REPLACE';
  WRITE 3617 '# EL BX-RE BX-IM BY-RE BY-IM';
  LOOP I 1 NEL;
    MAKEMAP I; OPENF 935 WHERE&'MAP'&MRK(I) 'REPLACE'; PM 935; CLOSEF 935;
    GT MAP F MU A B G R;
    B(1) := CM(CONS(B(1))); B(2) := CM(CONS(B(2)));
    WRITE 3617 SF(I,'(I6)')&SF(B(1)|1,'(E15.7)')&SF(B(1)|2,'(E15.7)')&SF(B(2)|1,'(E15.7)')&SF(B(2)|2,'(E15.7)');
  ENDLOOP;
  CLOSEF 3617;

ENDPROCEDURE;

RUN; END;
