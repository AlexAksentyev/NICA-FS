INCLUDE 'bin/setups/SECOND-ST/SEQFULL';

PROCEDURE INJECT NUM PSI_DEG; {at injection: SX = 0, SY, SZ = sin PSI, cos PSI}
  VARIABLE X 100; VARIABLE I 1;
  VARIABLE PSI 1; VARIABLE SY 1; VARIABLE SZ 1;
  PSI := DEG2RAD(PSI_DEG); {PSI IN RADIANS}
  X := LINSPACE(-3E-3, 3E-3, NUM);
  SY := SIN(PSI); SZ := COS(PSI);
  SR 0 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
  LOOP I 1 NUM;
    SR X|I 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
    SR 0 0 X|I 0 0 0 0 0 1; SSR 0 SY SZ;
    SR 0 0 0 0 0 (X|I)/10 0 0 1; SSR 0 SY SZ;
  ENDLOOP;
ENDPROCEDURE;

PROCEDURE MAIN MOM {MeV/c};
  VARIABLE WHERE 100; VARIABLE MRKR 100; VARIABLE PSI0_DEG 1;
  VARIABLE PNUM 1;
  {LATTICE ARGUMENTS}
  VARIABLE KS 1 2; VARIABLE X 1 2;  VARIABLE XS 1 2;
  VARIABLE NAVIPSI_DEG 1; 
  {for TREL}
  VARIABLE MAPPARAMS 1 6; {HOLDS THE ABOVE VALUES}
  VARIABLE MAPARR 1000 6 547; VARIABLE SPNRARR 1000 3 3 547;
  VARIABLE VRF 1 1 1;
  VARIABLE FREQ 1;
  VARIABLE HNUM 1;
  VARIABLE RFFLAG 1;
  VARIABLE NSEQ 1; {number of elements in lattice}
  VARIABLE NTURN 1; {number of turns in the accelerator}

  RFFLAG := 1;
  NSEQ := 546;
  HNUM := 66;
  VRF(1, 1) := 100/HNUM; {RF Voltage [kV]}
  FREQ := HNUM*REVFREQ(fACCLEN(1)); {RF Frequency}
  {********** CONTROLS **********}
  NTURN := 150; {number of turns to track}
  PNUM := 10; {number of particles in a bunch}
  NAVIPSI_DEG := 0; {navigator set angle between the z and spin axes}
  PSI0_DEG := NAVIPSI_DEG; {angle between the spin orientation at injection and the z-axis}
  {******************************}
  MRKR := 'PSI0spin-'&MRK(PSI0_DEG); {marker to identify data files}
  WRITE 6 '^^^^ INITIAL SPIN PSI'&SF(PSI0_DEG,'(F15.7)');
  DIRSET WHERE 'data/SECOND-ST/MOMVARY/'&MRK(MOM)&'MeV:c/SEQ/'&MRK(NTURN)&'/NAVIPSI-'&MRK(NAVIPSI_DEG)&'/';
  GROUTF 'img/dump/TR' 1;
  
  
  OV 3 3 0;
  DAEPS 1E-12; DIRSET_AUTO 1;
  { SET lattice parameters }
  SET_FOR_PROTONS MOM X XS;
  NAVIGATORS 3e-2 NAVIPSI_DEG KS;
  MAPPARAMS(1) := X(1);   MAPPARAMS(2) := X(2);   {X}
  MAPPARAMS(3) := XS(1);  MAPPARAMS(4) := XS(2);  {XS}
  MAPPARAMS(5) := KS(1);  MAPPARAMS(6) := KS(2);  {KS}  
  
  LATTICE MAPPARAMS MAPARR SPNRARR;
  IF RFFLAG=1;
    UM; RF VRF 0 FREQ 0 0.05;
    INSERT 1 MAPARR SPNRARR NSEQ;
    NSEQ := NSEQ + 1;
  ENDIF;

  INJECT PNUM PSI0_DEG;     {PSI at SPD}
  WRITE 6 '******************** STARTING TRACKING';
  OPENF 99 WHERE&'PRAY:'&MRKR&'.dat' 'REPLACE';
  PRAY 99; CLOSEF 99;
  OPENF 772 WHERE&'TRPRAY:'&MRKR&'.dat' 'REPLACE';
  OPENF 893 WHERE&'TRPSPI:'&MRKR&'.dat' 'REPLACE';
  TRPRAY 772; TRPSPI 893;
  TREL MAPARR SPNRARR 1 NSEQ NTURN 772 893;
  CLOSEF 772; CLOSEF 893;

ENDPROCEDURE; {MAIN}



PROCEDURE RUN;
  VARIABLE MOM 1 6; {MeV/c} VARIABLE CASE 1;
    MOM(1) := 511; {the proton EDM experiment momentum (EKIN=130 MeV)}
    MOM(2) := 800; {filler value}
    MOM(3) := 1.6*1000; {1ST and 2ST modes maximum momenta...}
    MOM(4) := 3.2*1000; {... in MeV/c respectively}
    MOM(5) := 4800;
    MOM(6) := 6400;
    LOOP CASE 1 6;
      MAIN MOM(CASE);
    ENDLOOP;
ENDPROCEDURE;
RUN; END;
