INCLUDE 'bin/setups/BYPASS/FULL_clean';

PROCEDURE INJECT NUM PSI_DEG; {at injection: SX = 0, SY, SZ = sin PSI, cos PSI}
  VARIABLE X 100; VARIABLE I 1;
  VARIABLE PSI 1; VARIABLE SY 1; VARIABLE SZ 1;
  PSI := DEG2RAD(PSI_DEG); {PSI IN RADIANS}
  X := LINSPACE(-1E-3, 1E-3, NUM);
  SY := SIN(PSI); SZ := COS(PSI);
  SR 0 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
  LOOP I 1 NUM;
    SR X|I 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
    SR 0 0 X|I 0 0 0 0 0 1; SSR 0 SY SZ;
    SR 0 0 0 0 0 (X|I)/10 0 0 1; SSR 0 SY SZ;
  ENDLOOP;
ENDPROCEDURE;

PROCEDURE MAIN;
  VARIABLE WHERE 100; VARIABLE MRKR 10;
  VARIABLE GAMMA 1;
  VARIABLE MAPSE 1;

  VARIABLE PNUM 1;
  VARIABLE PSI0_DEG 1;
  VARIABLE NTURN 1;

  MRKR := 'FULL';
  DIRSET WHERE 'data/BYPASS/';
  GROUTF 'img/dump/TR' 1;
  GAMMA := 1.129;
  NTURN := 30000;
  PSI0_DEG := 0;
  PNUM := 10;
  
  
  OV 3 3 0;
  DAEPS 1E-10; {this sets DA garbage collection tolerance so that the TRANSFER MAP doesn't get the 1e-15 ABERRATION coefficient}
  { SET lattice parameters }
  SET_FOR_DEUTERONS GAMMA;
  LATTICE 1;
     {print transfer map}
      OPENF 636 WHERE&'TrMAP:'&MRKR 'REPLACE';
      PM 636; CLOSEF 636;
      {pront spin transfer map}
      OPENF 636 WHERE&'SpTrMAP:'&MRKR 'REPLACE';
      PSM 636; CLOSEF 636;
      {print aberrations}
      OPENF 636 WHERE&'ABERRATIONS:'&MRKR 'REPLACE';
      PA 636; CLOSEF 636;
      {other stats about the map}
      MAPSE := SE(MAP);
      OPENF 636 WHERE&'MAP-SYM-ERR:'&MRKR 'REPLACE';
      WRITE 636 MAPSE; CLOSEF 636;
      WRITE 6 'MAP SYMPLECTIFICATION ERROR' MAPSE;
      WRITE 6 'LATTICE LENGTH' SPOS;


  INJECT PNUM PSI0_DEG;
  WRITE 6 '******************** STARTING TRACKING';
  OPENF 99 WHERE&'PRAY:'&MRKR&'.dat' 'REPLACE';
  PRAY 99; CLOSEF 99;
  OPENF 772 WHERE&'TRPRAY:'&MRKR&'.dat' 'REPLACE';
  OPENF 893 WHERE&'TRPSPI:'&MRKR&'.dat' 'REPLACE';
  TRPRAY 772; TRPSPI 893;
  TR NTURN NINT(NTURN/5000) -1 -3 1.2 1.2 0 0 -12;
  CLOSEF 772; CLOSEF 893;

ENDPROCEDURE; {MAIN}

PROCEDURE RUN;
  MAIN;
ENDPROCEDURE;
RUN; END;
