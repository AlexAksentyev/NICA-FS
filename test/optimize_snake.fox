INCLUDE 'bin/setups/BENDS24/FULL';

PROCEDURE WMAT MAT N M OU;
  VARIABLE I 1; VARIABLE J 1;
  LOOP I 1 N;
    LOOP J 1 M;
      WRITE OU MAT(I,J);
      ENDLOOP;
    ENDLOOP;
ENDPROCEDURE; {WMAT}
FUNCTION FROB A M N;
  VARIABLE I 1; VARIABLE J 1;
  VARIABLE RES NM1;
  LOOP I 1 M;
    LOOP J 1 N;
    RES := RES + SQR(ABS(CONS(A(I,J))));
    ENDLOOP;
  ENDLOOP;
  FROB := RES;
ENDFUNCTION; {FROBENIUS NORM}
FUNCTION TRACE A M;
  VARIABLE I 1;
  LOOP I 1 M; TRACE := TRACE + CONS(A(I,I)); ENDLOOP;
ENDFUNCTION; {TRACE}
PROCEDURE MATDIFF33 A B D;
  VARIABLE I 1; VARIABLE J 1;
  LOOP I 1 3; LOOP J 1 3;
    D(I,J) := A(I,J) - B(I,J);
    ENDLOOP; ENDLOOP;
ENDPROCEDURE; {MATDIFF33}

PROCEDURE OPTIMIZE_SNAKE HZ1 HZ2 KZ1 KZ2 QF QD; {optimize the value of HZ1 to get SPNR as close as possible to the Identity}
  VARIABLE I33 1000 3 3; VARIABLE DMAT 1000 3 3;
  VARIABLE I 1; VARIABLE J 1; VARIABLE OBJ 1000;
  VARIABLE CNT 1;
  
  EYE I33 3;

  FIT HZ1;
    HZ2 := HZ1;
    LATTICE HZ1 HZ2 KZ1 KZ2 QF QD 1;
    MATDIFF33 SPNR I33 DMAT;
    OBJ := CONS(FROB(DMAT, 3, 3)) ; {CONS(SQR(FROB(SPNR, 3, 3)) - 2*TRACE(SPNR, 3) + 3);}
    CNT := CNT + 1;
    WRITE 6 ST(CNT)&'    '&ST(OBJ)&'     '&ST(HZ1);
  ENDFIT 1E-18 1000 1 OBJ;
ENDPROCEDURE;  {OPTIMIZE}

PROCEDURE INJECT NUM;
  VARIABLE X 100; VARIABLE I 1;
  CR;
  X := LINSPACE(-1E-3, 1E-3, NUM);
  LOOP I 1 NUM;
    SR X|I 0 0 0 0 0 0 0 1; SSR 0 0 1;
    SR 0 0 X|I 0 0 0 0 0 1; SSR 0 0 1;
    SR 0 0 0 0 0 (X|I)/10 0 0 1; SSR 0 0 1;
  ENDLOOP;
ENDPROCEDURE;

PROCEDURE MAIN PARTICLE;
  VARIABLE WHERE 100;
  VARIABLE QF  1; VARIABLE QD  1;
  VARIABLE HZ1 1; VARIABLE HZ2 1;
  VARIABLE KZ1 1; VARIABLE KZ2 1;

  DIRSET WHERE 'data/OPTIMIZE_SNAKE/'&PARTICLE&'/';
  GROUTF 'img/dump/TR' 1;

  OV 3 3 0;
  IF PARTICLE='DEUTERON';
    SET_FOR_DEUTERONS HZ1 HZ2 QF QD;
    HZ1 := 0.4363977016105673; {previously found optimal value}
  ELSEIF PARTICLE='PROTON';
    SET_FOR_PROTONS HZ1 HZ2 QF QD;
    HZ1 := 0.1339129939629264; {previously found optimal value}
  ELSEIF TRUE;
    WRITE 6 '---------- UNRECOGNIZED PARTICLE NAME';
    QUIT 1;
  ENDIF;

  OPTIMIZE_SNAKE HZ1 HZ2 KZ1 KZ2 QF QD;
  WRITE 6 '++++++++++ OPTIMIZED VALUE ++++++++++'
                             HZ1;
  OPENF 9357 WHERE&'SNAKE_STR.dat' 'REPLACE';
  WRITE 9357 HZ1; CLOSEF 9357;

  IF FALSE;
    {THIS IS A CHECK ON THE DEUTERON CASE THAT THE FOUND VALUE GIVES FS FOR 3E6 TURNS AT LEAST}
    HZ1 := 0.4363977016105673; HZ2 := HZ1;
    LATTICE HZ1 HZ2 KZ1 KZ2 QF QD 1;
    { INJECT 30; }
    CR;
    SR 0 0 0 0 0 0 0 0 1; SSR 0 0 1;
    SR 0 0 0 0 0 0 0 0 1; SSR 0 1 0;
    SR 0 0 0 0 0 0 0 0 1; SSR 1 0 0;


    OPENF 924 WHERE&'TRPRAY.dat' 'REPLACE';
    OPENF 925 WHERE&'TRPSPI.dat' 'REPLACE';
      TRPRAY 924; TRPSPI 925;
      TR 3000000 10000 -1 -3 1.2 1.2 0 0 -12;
    CLOSEF 924; CLOSEF 925;
  ENDIF;

ENDPROCEDURE; {MAIN}

PROCEDURE RUN;
 MAIN 'PROTON';
 MAIN 'DEUTERON';
ENDPROCEDURE;

RUN; END;

