INCLUDE 'bin/setups/NICA_FS';

PROCEDURE RUN;
  VARIABLE WHERE 100;
  VARIABLE QF  1; VARIABLE QD  1;
  VARIABLE HZ1 1; VARIABLE HZ2 1;
  VARIABLE KZ1 1; VARIABLE KZ2 1;
  VARIABLE I 1; VARIABLE J 1; VARIABLE RES 100;
  PROCEDURE INJECT NUM;
    VARIABLE X 100; { VARIABLE Y 100; } VARIABLE I 1;
    { X := VEGAUSS(0, 1e-3, NUM, WHERE&'X'); }
    { Y := VEGAUSS(0, 1e-3, NUM, WHERE&'Y'); }
    X := LINSPACE(-1e-3, 1e-3, NUM);
    CR;
    LOOP I 1 NUM;
      SR X|I 0 0 0 0 0 0 0 1; SSR 0 0 1;
      SR 0 0 X|I 0 0 0 0 0 1; SSR 0 0 1;
      SR X|I 0 X|I 0 0 0 0 0 1; SSR 0 0 1;
      SR 0 0 0 0 0 X|I 0 0 1; SSR 0 0 1;
    ENDLOOP;
  ENDPROCEDURE; {INJECT}
  PROCEDURE INJECT_SPIN NUM;
    VARIABLE I 1; VARIABLE PHI 1001;
    VARIABLE SX 1001; VARIABLE SY 1001;
    PHI := LINSPACE(0, 2*PI, NUM);
    SX := COS(PHI);
    SY := SIN(PHI);
    CR;
    LOOP I 1 NUM;
      SR 0 0 0 0 0 0 0 0 1; SSR SX|I SY|I 0;
      SR 0 0 0 0 0 0 0 0 1; SSR 0 0 1;
      SR 0 0 0 0 0 0 0 0 1; SSR .25 .25 SQRT(1 - SQR(.25)*2);
      SR 0 0 0 0 0 0 0 0 1; SSR 0 SY|I SX|I;
    ENDLOOP;
  ENDPROCEDURE; {INJECT_SPIN}


  DIRSET WHERE 'data/DECOH/SHORT/INJECT_SPIN/OPTIMAL/' TRUE;
  GROUTF 'img/dump/TR' 1;

  OV 5 3 0;
  RP 270 1876.5592/AMUMEV 1;
  RPS 1 -.142987;

  {LATTICE PARAMETERS FOR THE DEUTERON CASE:}
  { * NAVIGATOR SOLENOIDS ARE TURNED OFF}
  { * SNAKE FIELD STRENGTHS ARE COMPUTED ACCORDING TO EQ (1)}
  { * QUADRUPOLE GRADIENTS TAKEN FROM THE COMMENTS FILE}
  QF:=-.044; QD:=-.032;
  HZ1:= 1/12 * PI/(1+G0) * 1/.7;
  HZ1 := 0.4363977149403208; {OPTIMIZED VALUE}
  HZ2 := HZ1;

  WRITE 6 '';
  WRITE 6 'DEUTERON G = '&ST(G0);
  WRITE 6 'HZ1 = '&ST(HZ1);
  WRITE 6 'HZ2 = '&ST(HZ2);

  BENDS24_SETUP HZ1 HZ2 KZ1 KZ2 QF QD;

  OPENF 100103 WHERE&'SPNR.map' 'REPLACE';
    LOOP I 1 3; RES := '';
      LOOP J 1 3; RES := RES&'     '&ST(CONS(SPNR(I, J)));ENDLOOP;
      WRITE 6 RES; WRITE 100103 RES;
    ENDLOOP;
  CLOSEF 100103;

  INJECT_SPIN 10;
  TRACK 1000 1 WHERE '';
  
  WRITE 6 'SUCCESS!';

ENDPROCEDURE; {RUN}

RUN; END;

