INCLUDE 'bin/setups/BYPASS/FULL_SEX_wRC';

PROCEDURE INJECT NUM PSI_DEG; {at injection: SX = 0, SY, SZ = sin PSI, cos PSI}
  VARIABLE X 100; VARIABLE I 1;
  VARIABLE PSI 1; VARIABLE SY 1; VARIABLE SZ 1;
  PSI := DEG2RAD(PSI_DEG); {PSI IN RADIANS}
  X := LINSPACE(-1E-3, 1E-3, NUM);
  SY := SIN(PSI); SZ := COS(PSI);
  SR 0 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
  LOOP I 1 NUM;
    SR X|I 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
    SR 0 0 X|I 0 0 0 0 0 1; SSR 0 SY SZ;
    SR 0 0 0 0 0 (X|I)/10 0 0 1; SSR 0 SY SZ;
  ENDLOOP;
ENDPROCEDURE;

PROCEDURE OUTPUT_MAP_DATA WHERE MRKR;
  VARIABLE MU 800; VARIABLE NBAR 800 3; VARIABLE EL 1;
  
  TSS MU NBAR 0;
  OPENF 3618 WHERE&'NU:'&MRKR&'.da' 'REPLACE'; WRITE 3618 MU; CLOSEF 3618;
  LOOP EL 1 3;
    OPENF 3618 WHERE&'NBAR'&MRK(EL)&':'&MRKR&'.da' 'REPLACE';
      WRITE 3618 NBAR(EL);
      CLOSEF 3618;
  ENDLOOP;
  {print transfer map}
  OPENF 636 WHERE&'TrMAP:'&MRKR 'REPLACE';
  PM 636; CLOSEF 636;
  {print spin transfer map}
  OPENF 636 WHERE&'SpTrMAP:'&MRKR 'REPLACE';
  PSM 636; CLOSEF 636;
  {print aberrations}
  OPENF 636 WHERE&'ABERRATIONS:'&MRKR 'REPLACE';
  PA 636; CLOSEF 636;
ENDPROCEDURE; {map data output}

FUNCTION TURNNUM NUM;
    VARIABLE KILOS 1; VARIABLE MILS 1;
    KILOS := NUM/1000;
    MILS := NUM/1000000;
    IF MILS<1;
      TURNNUM := MRK(KILOS)&'K';
    ELSEIF LO(1);
      TURNNUM := MRK(MILS)&'M';
    ENDIF;
  ENDFUNCTION;

PROCEDURE MAIN comNU TILTS; { compensator NU input (to be multiplied by Wcyc in lattice.fox for W in rad/sec }
  VARIABLE WHERE 100; VARIABLE MRKR 50; {file metadata}

  {for tracking, pertaining to particles}
  VARIABLE PNUM 1; {particle number}
  VARIABLE PSI0_DEG 1; {initial spin orientation}
  VARIABLE NTURN 1; {turns to track}

  {lattice parameters}
  VARIABLE EBE 1; {EB element's E-field value}
  VARIABLE SGX 1; VARIABLE SGY 1; {sextupole ggradients}

  MRKR := 'RCNU_'&MRK(ABS(comNU*1e3));
  WRITE 6 'RC NU : '&ST(comNU); WRITE 6 'MARKER:' MRKR;
  DIRSET WHERE 'data/BYPASS_SEX_wRC/RC-vary/'; DIRSET_AUTO 1;
  GROUTF 'img/dump/TR' 1;

  WRITE 6 'TILTS' TILTS;

  NTURN := 30000; 
  PSI0_DEG := 45;
  PNUM := 10;

  EBE := 99.1475; {kV/cm; gives (supposedly) spin-tune = 1.5e-6 (...75) or 7e-8 (...85)}
  SGX := 0.0475358; SGY := -0.0124713; {optimal sextupole values (at this point)}

  OV 3 3 0;
  DAEPS 1E-12; {this sets DA garbage collection tolerance so that the TRANSFER MAP doesn't get the 1e-15 ABERRATION coefficient}
  { SET lattice parameters }
  SET_FOR_DEUTERONS 1.1279235;
  LATTICE SGX SGX SGY
          EBE
          comNU TILTS;
  OPENF 9292 WHERE&'LATTICE-PARAMETERS:'&MRKR 'REPLACE';
    WRITE 9292 '    SEXT-GX         SEXT-GY          EBE          RCNU ';
    WRITE 9292 SF(SGX, '(F15.7)')&SF(SGY, '(F15.7)')&SF(EBE, '(F15.7)')&SF(comNU, '(F15.7)');
    CLOSEF 9292;
  OUTPUT_MAP_DATA WHERE MRKR;
    WRITE 6 'LATTICE LENGTH' SPOS;


  {INJECT PNUM PSI0_DEG;
  WRITE 6 '******************** STARTING TRACKING';
  OPENF 99 WHERE&'PRAY:'&MRKR&'.dat' 'REPLACE';
  PRAY 99; CLOSEF 99;
  OPENF 772 WHERE&'TRPRAY:'&MRKR&'.dat' 'REPLACE';
  OPENF 893 WHERE&'TRPSPI:'&MRKR&'.dat' 'REPLACE';
  TRPRAY 772; TRPSPI 893;
  TR NTURN NINT(NTURN/5000) -1 -3 1.2 1.2 0 0 -12;
  CLOSEF 772; CLOSEF 893;}

ENDPROCEDURE; {MAIN}

PROCEDURE OPTIMIZER CASE;
  VARIABLE WHERE 100; VARIABLE MRKR 50; {file metadata}

  {lattice parameters}
  VARIABLE EBE 1; {EB element's E-field value}
  VARIABLE SGX 1; VARIABLE SGY 1; {sextupole gradients}
  VARIABLE TILTS 12;
  VARIABLE MU 800; VARIABLE NBAR 800 3;
  VARIABLE RCNU 1; VARIABLE LCNU 1; {to be optimized} VARIABLE OBJ 1; {measure}

  MRKR := MRK(CASE);
  WRITE 6 'MARKER:' MRKR;
  DIRSET WHERE 'data/BYPASS_SEX_wRC/RLC-opt-to/'; DIRSET_AUTO 1;
  GROUTF 'img/dump/TR' 1;

  EBE := 99.1485; {kV/cm; gives (supposedly) spin-tune = 1.5e-6 (...75) or 7e-8 (...85)}
  SGX := 0.0475358; SGY := -0.0124713; {optimal sextupole values (at this point)}


  OV 3 3 0;
  DAEPS 1E-12; {this sets DA garbage collection tolerance so that the TRANSFER MAP doesn't get the 1e-15 ABERRATION coefficient}
  { SET lattice parameters }
  SET_FOR_DEUTERONS 1.1279235;
  IF CASE=0; TILTS := VEGAUSS(0, 0, 12, WHERE&'EB_GAUSS:'&MRKR);
  ELSEIF LO(1);
    TILTS := VEGAUSS((CASE-1)*0.5e-4, 1E-4, 12, WHERE&'EB_GAUSS:'&MRKR);
  ENDIF;

  FIT RCNU LCNU;
    LATTICE SGX SGX SGY
            EBE
            RCNU LCNU TILTS;
    TSS MU NBAR 0;
    OBJ :=  ABS(CONS(NBAR(2))-1); {the compensator must reduce both the N_x and N_z on the CO to zero}
   ENDFIT 1E-5 1000 1 OBJ;
          
  OPENF 9292 WHERE&'LATTICE-PARAMETERS:'&MRKR 'REPLACE';
    WRITE 9292 '    SEXT-GX         SEXT-GY          EBE          RCNU           LCNU ';
    WRITE 9292 SF(SGX, '(F15.7)')&SF(SGY, '(F15.7)')&SF(EBE, '(F15.7)')&SF(RCNU, '(F15.7)')&SF(LCNU, '(F15.7)');
    CLOSEF 9292;
  OUTPUT_MAP_DATA WHERE MRKR;
  
ENDPROCEDURE; {OPTIMIZER}

PROCEDURE RUNMAIN;
  VARIABLE TILTS 12; {EB elements' tilts vector}
  VARIABLE I 1; VARIABLE UNITFORCE 1; {unit SPIN TUNE increase in RADC}
  TILTS := VEGAUSS(0.5e-4, 1E-4, 12, 'data/BYPASS_SEX_wRC/EB_GAUSS:ALL'); {in radians}  { <- ensure mean tilt value @ half RMS }
  UNITFORCE := -1.2E-3;
  LOOP I 1 50;
    MAIN (I-1)*UNITFORCE TILTS; ENDLOOP;
ENDPROCEDURE; {RUNMAIN}
PROCEDURE RUN;
  VARIABLE CASE 1;
  LOOP CASE 0 5;
    OPTIMIZER CASE;
  ENDLOOP;
ENDPROCEDURE;
RUN; END;
