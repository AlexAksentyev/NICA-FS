INCLUDE 'bin/setups/NICA_FS';

PROCEDURE RUN;
  VARIABLE WHERE 100;
  VARIABLE QF  1; VARIABLE QD  1;
  VARIABLE HZ1 1; VARIABLE HZ2 1;
  VARIABLE KZ1 1; VARIABLE KZ2 1;
  PROCEDURE INJECT NUM;
    VARIABLE X 100; VARIABLE I 1;
    X := LINSPACE(-1e-3, 1e-3, NUM);
    CR;
    LOOP I 1 NUM;
      SR X|I 0 0 0 0 0 0 0 1; SSR 0 0 0;
      SR 0 0 X|I 0 0 0 0 0 1; SSR 0 0 0;
    ENDLOOP;
  ENDPROCEDURE; {INJECT}
  PROCEDURE INJECT_SPIN NUM;
    VARIABLE I 1; VARIABLE SX 100; VARIABLE SY 100;
    SX := LINSPACE(-1, 1, NUM);
    SY := SQRT(1 - SX*SX);
    CR;
    LOOP I 1 NUM;
      SR 0 0 0 0 0 0 0 0 1; SSR SX|I SY|I 0;
    ENDLOOP;
  ENDPROCEDURE;
  PROCEDURE TRACK PNTNUM STEP FILEDIR MARK;
    VARIABLE TURN 1; VARIABLE PNT 1; VARIABLE PCT 1;

    OPENF 100500 FILENAME(FILEDIR, 'PRAY', 'TR'&MARK) 'REPLACE';
    PRAY 100500; CLOSEF 100500;
    WRITE 6 SI(0)&' %';
    OPENF 100501 FILENAME(FILEDIR, 'TRPSPI', 'TR'&MARK) 'REPLACE';
    WRITE 100501 '# TRACKING OUTPUT';
    WRITE 100501 'TURN PID S_X S_Y S_Z';
    WRITETBL 100501 SPI 3 SF(0, '(I15)');
    OPENF 100502 FILENAME(FILEDIR, 'TRPRAY', 'TR'&MARK) 'REPLACE';
    WRITE 100502 '# TRACKING OUTPUT';
    WRITE 100502 'TURN PID X A Y B T D';
    WRITETBL 100502 RAY 6 SF(0, '(I15)');

    TRR 1;
    LOOP PNT 1 PNTNUM; TURN := STEP*PNT;
      TR STEP -STEP -1 -3 1.1 1.1 0 0 -12; {TURN OFF *** SYMPLECTIFICATION *** (-21) FOR NICA_FULL}
      WRITETBL 100501 SPI 3 SF(TURN, '(I15)');
      WRITETBL 100502 RAY 6 SF(TURN, '(I15)');
      PCT := 100*PNT/PNTNUM;
      IF MOD(PCT, 10)=0; WRITE 6 SI(PCT)&' %'; ENDIF;
    ENDLOOP;

    CLOSEF 100500; CLOSEF 100501; CLOSEF 100502;
  ENDPROCEDURE; {TRACK}

  DIRSET WHERE 'data/TEST/' TRUE;
  GROUTF 'img/dump/TR' 1;

  OV 1 3 0;
  RP 270 1876.5592/AMUMEV 1;
  RPS 1 -.142987;

  {LATTICE PARAMETERS FOR THE DEUTERON CASE:}
  { * NAVIGATOR SOLENOIDS ARE TURNED OFF}
  { * SNAKE FIELD STRENGTHS ARE COMPUTED ACCORDING TO EQ (1)}
  { * QUADRUPOLE GRADIENTS TAKEN FROM THE COMMENTS FILE}
  QF:=-.044; QD:=-.032;
  HZ1:= 1/12 * PI/(1+G0) * 1/.7;
  HZ2 := HZ1;

  WRITE 6 '';
  WRITE 6 'DEUTERON G = '&ST(G0);
  WRITE 6 'HZ1 = '&ST(HZ1);
  WRITE 6 'HZ2 = '&ST(HZ2);

  BENDS24_SETUP HZ1 HZ2 KZ1 KZ2 QF QD;

  { FULL_SETUP; }

  INJECT_SPIN 100;
  TRACK 1000 1000 WHERE '';
  
  WRITE 6 'SUCCESS!';

ENDPROCEDURE; {RUN}

RUN; END;

