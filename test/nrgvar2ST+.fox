INCLUDE 'bin/setups/2ST+/FULL';

PROCEDURE INJECT NUM PSI_DEG; {at injection: SY = 0,; SX, SZ = sin PSI, cos PSI}
  VARIABLE X 100; VARIABLE I 1;
  VARIABLE PSI 1; VARIABLE SX 1; VARIABLE SZ 1;
  PSI := DEG2RAD(PSI_DEG); {PSI IN RADIANS}
  X := LINSPACE(-3E-3, 3E-3, NUM);
  SX := SIN(PSI); SZ := COS(PSI);
  SR 0 0 0 0 0 0 0 0 1; SSR SX 0 SZ;
  LOOP I 1 NUM;
    SR X|I 0 0 0 0 0 0 0 1; SSR SX 0 SZ;
    SR 0 0 X|I 0 0 0 0 0 1; SSR SX 0 SZ;
    SR 0 0 0 0 0 (X|I)/30 0 0 1; SSR SX 0 SZ;
  ENDLOOP;
ENDPROCEDURE;

PROCEDURE MAIN yG {the resonance integer};
  VARIABLE WHERE 100;
  VARIABLE PSI0_DEG 1; VARIABLE MRKR 100; VARIABLE PNUM 1;
  VARIABLE MAPSE 1;
  VARIABLE MU 800; VARIABLE NBAR 800 3; VARIABLE I 1;
  {LATTICE ARGUMENTS}
  VARIABLE KS 1 2; VARIABLE X  1 2; VARIABLE XS 1 2;
  VARIABLE NTURN 1;
  VARIABLE NAVIPSI_DEG 1; VARIABLE FREEZE 1;
  {********** CONTROLS **********}
  NTURN := 30000000;
  PNUM := 10; {number of particles in a bunch}
  NAVIPSI_DEG := 30; {navigator defined angle between the z and spin axes}
  PSI0_DEG := NAVIPSI_DEG + 10; {angle between the spin orientation at injection and the z-axis}
  {******************************}
  MRKR := 'PSI0spin-'&MRK(PSI0_DEG); {marker to identify data files}
  WRITE 6 '^^^^ INITIAL SPIN PSI'&SF(PSI0_DEG,'(F15.7)');
  IF yG=0; FREEZE := LO(1); {turn on the spin-freezing snake solenoid capability}
  ELSEIF yG>0; FREEZE := LO(0); {turn off the spin-freezing, use snakes as pure navigator solenoids}
  ENDIF;
  DIRSET WHERE 'data/2ST+/yGVARY/'&MRK(yG)&'/'&MRK(NTURN)&'/NAVIPSI-'&MRK(NAVIPSI_DEG)&'/';
  GROUTF 'img/dump/TR' 1;
  
  
  OV 3 3 0;
  DAEPS 1E-12; {this sets DA garbage collection tolerance so that the TRANSFER MAP doesn't get the 1e-15 ABERRATION coefficient}
  DIRSET_AUTO 1;
  { SET lattice parameters }
  SET_FOR_PROTONS yG X XS;
  NAVIGATORS 3e-2 NAVIPSI_DEG KS;
  LATTICE X XS KS 1;
     {print transfer map}
      OPENF 636 WHERE&'TrMAP:'&MRKR 'REPLACE';
      PM 636; CLOSEF 636;
      {prInt spin transfer map}
      OPENF 636 WHERE&'SpTrMAP:'&MRKR 'REPLACE';
      PSM 636; CLOSEF 636;
      {print aberrations}
      OPENF 636 WHERE&'ABERRATIONS:'&MRKR 'REPLACE';
      PA 636; CLOSEF 636;
      {other stats about the map}
      MAPSE := SE(MAP);
      OPENF 636 WHERE&'MAP-SYM-ERR:'&MRKR 'REPLACE';
      WRITE 636 MAPSE; CLOSEF 636;
      WRITE 6 'MAP SYMPLECTIFICATION ERROR' MAPSE;
  TSS MU NBAR 0;
      OPENF 636 WHERE&'MU:'&MRKR 'REPLACE';
      WRITE 636 MU; CLOSEF 636;
      LOOP I 1 3; OPENF 636 WHERE&'NBAR('&MRK(I)&'):'&MRKR 'REPLACE';
      WRITE 636 NBAR(I); CLOSEF 636; ENDLOOP;

  INJECT PNUM PSI0_DEG;
  WRITE 6 '******************** STARTING TRACKING';
  OPENF 99 WHERE&'PRAY:'&MRKR&'.dat' 'REPLACE';
  PRAY 99; CLOSEF 99;
  OPENF 772 WHERE&'TRPRAY:'&MRKR&'.dat' 'REPLACE';
  OPENF 893 WHERE&'TRPSPI:'&MRKR&'.dat' 'REPLACE';
  TRPRAY 772; TRPSPI 893;
  TR NTURN NINT(NTURN/5000) -1 -3 1.2 1.2 0 0 -12;
  CLOSEF 772; CLOSEF 893;

ENDPROCEDURE; {MAIN}

PROCEDURE RUN;
  VARIABLE yG 1 6; {integer} VARIABLE CASE 1;
  yG(1) := 0;  {WONT WORK!!!}
  yG(2) := 2; {adapted ONLY for THIS CASE yet}
  yG(3) := 6; 
  yG(4) := 7; 
  yG(5) := 10;
  yG(6) := 12;
  LOOP CASE 3 3;
    MAIN yG(CASE);
  ENDLOOP;
ENDPROCEDURE;
RUN; END;
