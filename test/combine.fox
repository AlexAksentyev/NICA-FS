INCLUDE 'bin/support/support';


PROCEDURE RUN;
  VARIABLE WHERE 100;
  VARIABLE MAPARR 1000 6 10; VARIABLE SPNRARR 1000 3 3 10;
  VARIABLE F 100 6; VARIABLE A 100 2; VARIABLE B 100 2;
  VARIABLE G 100 2; VARIABLE R 100 2; VARIABLE MU 100 2; 
  PROCEDURE LAT;
    DL .3; QUAD 1.85 0 0.3797649912; DL 5.25; QUAD .65 0 -.52319;
  ENDPROCEDURE;
  PROCEDURE PICK INDEX;
    VARIABLE I 1; VARIABLE J 1;
    LOOP I 1 TWOND; MSC(I) := MAPARR(I, INDEX); ENDLOOP;
    LOOP I 1 3; LOOP J 1 3; SSCR(I,J) := SPNRARR(I,J,INDEX) + 0*DD(1);
    ENDLOOP;ENDLOOP;
  ENDPROCEDURE;
  PROCEDURE MAKEMAP INDEX;
    VARIABLE J 1;
    WRITE 6 '* '&ST(INDEX);
    RMAPS INDEX MAPARR SPNRARR; {sets the MAP, SPNR transfer maps to those of the i-th element}
    LOOP J (INDEX+1) 4; WRITE 6 '**       '&ST(J);
      PICK J; LOCSET 0 0 0 0 0 0; UPDATE 1 1;
      ENDLOOP;
    LOOP J 1 (INDEX-1); WRITE 6 '**       '&ST(J);
      PICK J; LOCSET 0 0 0 0 0 0; UPDATE 1 1;
      ENDLOOP;
  ENDPROCEDURE;
  PROCEDURE SMAPS1 POS;
    SMAPS POS MAPARR SPNRARR;
  ENDPROCEDURE;

  DIRSET WHERE 'data/COMBINE/' TRUE;
  GROUTF 'img/dump/TR' 1;

  OV 1 2 0;
  RP 131 1 1;

  UM; LAT; OPENF 935 WHERE&'MAP0' 'REPLACE'; PM 935; CLOSEF 935;
  GT MAP F MU A B G R;
  { WRITE 6 B(1) B(2) '#'; }

  UM; DL 5.25; SMAPS1 1;
  UM; QUAD .65 0 -.52319; SMAPS1 2;
  UM; DL .3; SMAPS1 3;
  UM; QUAD 1.85 0 0.3797649912; SMAPS1 4;

  MAKEMAP 3;
  OPENF 935 WHERE&'MAP1' 'REPLACE'; PM 935; CLOSEF 935;
  GT MAP F MU A B G R;
  { WRITE 6 B(1) B(2) ' '; }

  RMAPS 3 MAPARR SPNRARR;
        PICK 4; LOCSET 0 0 0 0 0 0 ; UPDATE 1 1;
        PICK 1; LOCSET 0 0 0 0 0 0 ; UPDATE 1 1;
        PICK 2; LOCSET 0 0 0 0 0 0 ; UPDATE 1 1;
  OPENF 935 WHERE&'MAP2' 'REPLACE'; PM 935; CLOSEF 935;
ENDPROCEDURE;

RUN; END;
