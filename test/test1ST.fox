INCLUDE 'bin/setups/FIRST-ST/FULL';

PROCEDURE INJECT NUM;
  VARIABLE X 100; VARIABLE I 1;
  X := LINSPACE(-1E-3, 1E-3, NUM);
  SR 0 0 0 0 0 0 0 0 1; SSR 0 0 1;
  LOOP I 1 NUM;
    SR X|I 0 0 0 0 0 0 0 1; SSR 0 0 1;
    SR 0 0 X|I 0 0 0 0 0 1; SSR 0 0 1;
    SR 0 0 0 0 0 (X|I)/10 0 0 1; SSR 0 0 1;
  ENDLOOP;
ENDPROCEDURE;

PROCEDURE OPTIMIZE X2 X3 XM XS;
  VARIABLE KS 1 2; {initialized to zeros (NAVIs OFF)}
  VARIABLE X 1 2; VARIABLE OBJ 1;

  FIT X2 X3;
    X(1) := X2; X(2) := X3;
    UM; LATTICE X XM XS KS 0;
    OBJ := ABS(ME(1,2)) + ABS(ME(3,4));
    WRITE 6 'X2 [%] X3 [%] OBJ:' SF(X2*100,'(F15.7)')&SF(X3*100,'(F15.7)')&SF(OBJ,'(F15.7)');
  ENDFIT 1E-5 1000 1 OBJ ;
  
ENDPROCEDURE;

PROCEDURE MAIN;
  VARIABLE WHERE 100; VARIABLE MAPSE 1;
  {LATTICE ARGUMENTS}
  VARIABLE KS 1 2; VARIABLE X  1 2;
  VARIABLE XS 1 4; VARIABLE XM 1 4;
  {for optimization}
  VARIABLE X2 1; VARIABLE X3 1;
  VARIABLE NTURN 1;
  NTURN := 300000;
  DIRSET WHERE 'data/TEST/FIRST-ST/'&MRK(NTURN)&'/';
  GROUTF 'img/dump/TR' 1;
  
  
  OV 3 3 0;
  { SET lattice parameters }
  SET_FOR_PROTONS 130 X XM XS;
  NAVIGATORS 3e-2 0 1 KS;
  LATTICE X XM XS KS 1;
     {print transfer map}
      OPENF 636 WHERE&'TrMAP' 'REPLACE';
      PM 636; CLOSEF 636;
      {print aberrations}
      OPENF 636 WHERE&'ABERRATIONS' 'REPLACE';
      PA 636; CLOSEF 636;
      {other stats about the map}
      MAPSE := SE(MAP);
      WRITE 6 'MAP SYMPLECTIFICATION ERROR: ' MAPSE;


  INJECT 10;
  WRITE 6 '******************** STARTING TRACKING';
  OPENF 99 WHERE&'PRAY.dat' 'REPLACE';
  PRAY 99; CLOSEF 99;
  OPENF 772 WHERE&'TRPRAY.dat' 'REPLACE';
  OPENF 893 WHERE&'TRPSPI.dat' 'REPLACE';
  TRPRAY 772; TRPSPI 893;
  TR NTURN NINT(NTURN/5000) -1 -3 1.2 1.2 0 0 -12;
  CLOSEF 772; CLOSEF 893;

ENDPROCEDURE; {MAIN}

PROCEDURE RUN;
  MAIN;
ENDPROCEDURE;
RUN; END;
