INCLUDE 'bin/setups/BYPASS/SEQFULL_SEX_CLEAR';


PROCEDURE INJECT;
  CR;
  SR 0 0 0 0 0 0 0 0 1; SSR 0 0 1;
  SR 1E-3 0 0 0 0 0 0 0 1; SSR 0 0 1;
  { SR 0 0 1E-3 0 0 0 0 0 1; SSR 0 0 1; }
  SR 0 0 0 0 0 1E-4 0 0 1; SSR 0 0 1;
  SR .5E-3 0 0 0 0 0 0 0 1; SSR 0 0 1;
  SR 0 0 .5E-3 0 0 0 0 0 1; SSR 0 0 1;
ENDPROCEDURE;

PROCEDURE INJECT1 NUM;
  VARIABLE X 100; VARIABLE I 1;
  CR;
  X := LINSPACE(-1E-3, 1E-3, NUM);
  LOOP I 1 NUM;
    SR X|I 0 0 0 0 0 0 0 1; SSR 0 0 1;
    SR 0 0 X|I 0 0 0 0 0 1; SSR 0 0 1;
    SR 0 0 0 0 0 (X|I)/10 0 0 1; SSR 0 0 1;
  ENDLOOP;
ENDPROCEDURE;

PROCEDURE INJECT_SPIN NUM;
  VARIABLE T 100; VARIABLE C 100; VARIABLE S 100; VARIABLE I 1;
  T := LINSPACE(0, 2*PI, NUM);
  C := COS(T);
  S := SIN(T);
  CR;
  LOOP I 1 NUM;
    SR 0 0 0 0 0 0 0 0 1; SSR C|I S|I 0;
    SR 0 0 0 0 0 0 0 0 1; SSR C|I 0 S|I;
    SR 0 0 0 0 0 0 0 0 1; SSR 0 C|I S|I;
  ENDLOOP;
ENDPROCEDURE;


PROCEDURE RUN;
  VARIABLE WHERE 100;
  VARIABLE MAPPARAMS 1 5; {HOLDS THE ABOVE VALUES}
  VARIABLE MAPARR 1000 6 537; VARIABLE SPNRARR 1000 3 3 537;
  VARIABLE RFFLAG 1; VARIABLE NSEQ 1;
  VARIABLE VRF 1 1 1;
  VARIABLE FREQ 1;
  VARIABLE HNUM 1;
  VARIABLE GAMMA 1;

  {********** CONTROLS **********}
  GAMMA := 1.1279235; {beam injection energy}
  RFFLAG := 1; {insert RF at the beginning of lattice}
  NSEQ := 536; {number of elements in sequence PRIOR to the inserion}
  MAPPARAMS(4) := 99.1485; {optimum EB value; SEXTUPOLES off, BBE adjusted automatically}
  {******************************}

  DIRSET WHERE 'data/BYPASS_SEX_CLEAR/EL-BY-EL-TRACKING/';
  GROUTF 'img/dump/TR' 1;
  
  OV 3 3 0;
  DAEPS 1E-12; {this sets DA garbage collection tolerance so}
               {that the TRANSFER MAP doesn't get the 1e-15 ABERRATION coefficient}
  SET_FOR_DEUTERONS GAMMA;

  LATTICE MAPPARAMS MAPARR SPNRARR;
  IF RFFLAG=1;
    UM; RF VRF 0 FREQ 0 0.05;
    INSERT 1 MAPARR SPNRARR NSEQ;
    NSEQ := NSEQ + 1;
  ENDIF;

  INJECT1 10;

  OPENF 924 WHERE&'TRPRAY:SEQ.dat' 'REPLACE';
  OPENF 925 WHERE&'TRPSPI:SEQ.dat' 'REPLACE';
    TREL MAPARR SPNRARR 1 537 20 924 925;
  CLOSEF 924; CLOSEF 925;

ENDPROCEDURE;

RUN; END;
