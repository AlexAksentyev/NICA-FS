INCLUDE 'bin/setups/SECOND-ST/FULL';

PROCEDURE INJECT NUM PSI_DEG; {at injection: SX = 0, SY, SZ = sin PSI, cos PSI}
  VARIABLE X 100; VARIABLE I 1;
  VARIABLE PSI 1; VARIABLE SY 1; VARIABLE SZ 1;
  PSI := DEG2RAD(PSI_DEG); {PSI IN RADIANS}
  X := LINSPACE(-3E-3, 3E-3, NUM);
  SY := SIN(PSI); SZ := COS(PSI);
  SR 0 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
  LOOP I 1 NUM;
    SR X|I 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
    SR 0 0 X|I 0 0 0 0 0 1; SSR 0 SY SZ;
    SR 0 0 0 0 0 (X|I)/10 0 0 1; SSR 0 SY SZ;
  ENDLOOP;
ENDPROCEDURE;

PROCEDURE MAIN EKIN {MeV};
  VARIABLE WHERE 100;
  VARIABLE PSI0_DEG 1; VARIABLE MRKR 100; VARIABLE PNUM 1;
  VARIABLE MAPSE 1;
  {LATTICE ARGUMENTS}
  VARIABLE KS 1 2; VARIABLE X  1 2; VARIABLE XS 1 2;
  VARIABLE NTURN 1;
  VARIABLE NAVIPSI_DEG 1;
  {********** CONTROLS **********}
  NTURN := 30000;
  PNUM := 10; {number of particles in a bunch}
  NAVIPSI_DEG := 0; {navigator defined angle between the z and spin axes}
  PSI0_DEG := NAVIPSI_DEG; {angle between the spin orientation at injection and the z-axis}
  {******************************}
  MRKR := 'PSI0spin-'&MRK(PSI0_DEG); {marker to identify data files}
  WRITE 6 '^^^^ INITIAL SPIN PSI'&SF(PSI0_DEG,'(F15.7)');
  DIRSET WHERE 'data/SECOND-ST/NRGVARY/'&MRK(EKIN)&'MeV/'&MRK(NTURN)&'/NAVI-ON/NAVIPSI-'&MRK(NAVIPSI_DEG)&'/';
  GROUTF 'img/dump/TR' 1;
  
  
  OV 3 3 0;
  DAEPS 1E-12; {this sets DA garbage collection tolerance so that the TRANSFER MAP doesn't get the 1e-15 ABERRATION coefficient}
  { SET lattice parameters }
  SET_FOR_PROTONS EKIN X XS;
  NAVIGATORS 3e-2 NAVIPSI_DEG KS;
  LATTICE X XS KS 1;
     {print transfer map}
      OPENF 636 WHERE&'TrMAP:'&MRKR 'REPLACE';
      PM 636; CLOSEF 636;
      {pront spin transfer map}
      OPENF 636 WHERE&'SpTrMAP:'&MRKR 'REPLACE';
      PSM 636; CLOSEF 636;
      {print aberrations}
      OPENF 636 WHERE&'ABERRATIONS:'&MRKR 'REPLACE';
      PA 636; CLOSEF 636;
      {other stats about the map}
      MAPSE := SE(MAP);
      OPENF 636 WHERE&'MAP-SYM-ERR:'&MRKR 'REPLACE';
      WRITE 636 MAPSE; CLOSEF 636;
      WRITE 6 'MAP SYMPLECTIFICATION ERROR' MAPSE;

  INJECT PNUM PSI0_DEG;
  WRITE 6 '******************** STARTING TRACKING';
  OPENF 99 WHERE&'PRAY:'&MRKR&'.dat' 'REPLACE';
  PRAY 99; CLOSEF 99;
  OPENF 772 WHERE&'TRPRAY:'&MRKR&'.dat' 'REPLACE';
  OPENF 893 WHERE&'TRPSPI:'&MRKR&'.dat' 'REPLACE';
  TRPRAY 772; TRPSPI 893;
  TR NTURN NINT(NTURN/5000) -1 -3 1.2 1.2 0 0 -12;
  CLOSEF 772; CLOSEF 893;

ENDPROCEDURE; {MAIN}

FUNCTION MOM2NRG P {MeV/c};
  VARIABLE Mp 1; {proton rest mass}
  Mp := 938; {MeV}
  MOM2NRG := NINT(SQRT(SQR(P) + SQR(Mp))); {Ekin = SQRT(Pc^2 + Mp^2)}
ENDFUNCTION;

PROCEDURE RUN;
  VARIABLE NRG 1 6; {MeV} VARIABLE CASE 1;
  NRG(1) := 130; {the proton EDM experiment energy}
  NRG(2) := MOM2NRG(800); {filler value}
  NRG(3) := MOM2NRG(1.6*1000); {1ST and 2ST modes maximum momenta...}
  NRG(4) := MOM2NRG(3.2*1000); {... in MeV/c respectively}
  NRG(5) := MOM2NRG(4800);
  NRG(6) := MOM2NRG(6400);
  LOOP CASE 1 6;
    MAIN NRG(CASE);
  ENDLOOP;
ENDPROCEDURE;
RUN; END;
