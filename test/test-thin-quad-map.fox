INCLUDE 'bin/elements';

PROCEDURE RUN;
  VARIABLE WHERE 100;
  VARIABLE Q1 1; VARIABLE Q2 1; VARIABLE OBJ 1;
  VARIABLE MAPREV 800 4;
  PROCEDURE THINMQ1 L {m} TILT {rad} K1 {m^-2};
                 {see QUAD for parameter explanations}
  VARIABLE OM NM1;
    OM := SQRT(ABS(K1));
    UMS;
    {load thin quad orbital map into MSC}
    IF CONS(K1)>0;
      MSC(2) := -OM*SIN(OM*L)*DD(1) + 1*DD(2);
      MSC(4) := OM*SINH(OM*L)*DD(3) + 1*DD(4);
    ELSEIF CONS(K1)<0;
      MSC(2) := OM*SINH(OM*L)*DD(1) + 1*DD(2);
      MSC(4) := -OM*SIN(OM*L)*DD(3) + 1*DD(4);
    ELSEIF TRUE;
      DL L;
    ENDIF;
    {load thin quad spin map into SSCR}
    {*** NOT IMPLEMENTED yet, SSCR = Identity ***}
    {apply MSC and SSCR to MAP, SPNR respectively.}
    LOCSET 0 0 0 0 0 0; UPDATE 1 1;
  ENDPROCEDURE;
  PROCEDURE QUADR L K1;
    DL L/2; THINMQ1 L 0 K1; DL L/2;
  ENDPROCEDURE;


  DIRSET WHERE 'data/TEST-THIN-QUAD-MAP/' TRUE;
  GROUTF 'img/dump/TR' 1;

  OV 1 2 0;
  RP 131 1 1;

  SB .15 .15 0 .15 .15 0 0 0 0 0 0;
  Q1 := .5; Q2 := .5;
  FIT Q1 Q2;
    UM; CR; ER 1 3 1 3 1 1 1 1;
    BP;
      DL .2; QUADR 1 Q1; DL .2; QUADR 1 -1*Q2; DL .2;
    EP;
    PG -1 -2;
    OBJ := ABS(ME(1,2)) + ABS(ME(3,4));
    WRITE 6 Q1 Q2 OBJ;
  ENDFIT 1E-5 1000 1 OBJ;

ENDPROCEDURE; {RUN}

RUN; END;

