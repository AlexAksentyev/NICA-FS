{IN THIE SCRIPT I DO ELEMENT BY ELEMENT TRACKING OF}
{RAYS, ALL OF WHICH LIE ON THE REFERENCE ORBIT}
{AND DIFFER ONLY BY THEIR INITIAL ORIENTATION OF SPIN}



INCLUDE 'bin/support';

PROCEDURE RUN;
  VARIABLE WHERE 100;
  VARIABLE HZ1 1; VARIABLE HZ2 1;
  VARIABLE KZ1 1; VARIABLE KZ2 1;
  VARIABLE QF  1; VARIABLE QD  1;
  VARIABLE MAPPARAMS 1 6; {HOLDS THE ABOVE VALUES}
  VARIABLE MAPARR 1000 6 471; VARIABLE SPNRARR 1000 3 3 471;
  PROCEDURE INJECT NUM;
    VARIABLE X 100; VARIABLE I 1;
    X := LINSPACE(-1e-3, 1e-3, NUM);
    CR;
    LOOP I 1 NUM;
      SR X|I 0 0 0 0 0 0 0 1; SSR 0 0 1;
      SR 0 0 X|I 0 0 0 0 0 1; SSR 0 0 1;
    ENDLOOP;
  ENDPROCEDURE; {INJECT}
  PROCEDURE INJECT_SPIN NUM;
    VARIABLE I 1; VARIABLE PHI 1001;
    VARIABLE SX 1001; VARIABLE SY 1001;
    PHI := LINSPACE(0, 2*PI, NUM);
    SX := COS(PHI);
    SY := SIN(PHI);
    CR;
    LOOP I 1 NUM;
      SR 0 0 0 0 0 0 0 0 1; SSR SX|I SY|I 0;
    ENDLOOP;
  ENDPROCEDURE; {INJECT SPIN}


  DIRSET WHERE 'data/TEST/TREL/LONGIT_SPIN/' TRUE;
  GROUTF 'img/dump/TR' 1;

  OV 5 3 0;
  RP 270 1876.5592/AMUMEV 1;
  RPS 1 -.142987;

  WRITE 6 'B-RO = ' CHIM;

  {LATTICE PARAMETERS FOR THE DEUTERON CASE:}
  { * NAVIGATOR SOLENOIDS ARE TURNED OFF}
  { * SNAKE FIELD STRENGTHS ARE COMPUTED ACCORDING TO EQ (1)}
  { * QUADRUPOLE GRADIENTS TAKEN FROM THE COMMENTS FILE}
  QF:=-.044; QD:=-.032;
  HZ1:= 1/12 * PI/(1+G0) * 1/.7; {THEORETICAL VALUE}
  HZ1 := 0.4363977149403208; {OPTIMIZED VALUE}
  HZ2 := HZ1;
  
  MAPPARAMS(1) := HZ1; MAPPARAMS(2) := HZ2;
  MAPPARAMS(3) := KZ1; MAPPARAMS(4) := KZ2;
  MAPPARAMS(5) := QF;  MAPPARAMS(6) := QD;

  WRITE 6 '';
  WRITE 6 'DEUTERON G = '&ST(G0);
  WRITE 6 'HZ1 = '&ST(HZ1);
  WRITE 6 'HZ2 = '&ST(HZ2);

  MAPSEQ24BENDS MAPPARAMS MAPARR SPNRARR;

  INJECT 10;
  OPENF 7 WHERE&'TRPRAY:TREL.dat' 'REPLACE';
  OPENF 8 WHERE&'TRPSPI:TREL.dat' 'REPLACE';
  TREL MAPARR SPNRARR 1 471 1 7 8;
  CLOSEF 7; CLOSEF 8;
  
  WRITE 6 'SUCCESS!';

ENDPROCEDURE; {RUN}

RUN; END;

