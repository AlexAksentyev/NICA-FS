INCLUDE 'bin/setups/BYPASS/FULL_SEX_CLEAR';
  
PROCEDURE OPTIMIZE SGF1 SGF2 SGD EBE GAMMA;
  VARIABLE MU 800; VARIABLE NBAR 800 3;
  VARIABLE CHROM_X 100; VARIABLE CHROM_Y 100;
  VARIABLE OBJ 1; VARIABLE SF1 1; VARIABLE SF2 1; {focusing sextupoles in beta_x, D maxima}
                   VARIABLE SD 1; {defocusing sextupoles in beta_x maxima}
                   VARIABLE E1 1; {Wien-filter E-field}
  VARIABLE quadKx 1; VARIABLE quadKy 1; VARIABLE MU0 1;

  SF1 := SGF1; SF2 := SGF2; SD := SGD; E1 := EBE;
  FIT SF1 SF2 SD { E1 };
    LATTICE SF1 SF2 SD E1 0 1; {optimize assuming EBE=100 kV/cm (BBE irrelevant b/c R1=R2 in WIEN)}
    TSS MU NBAR 0;
    MU0 := CONS(MU);
    DAPEE MU 11 quadKx;
    DAPEE MU 33 quadKy;
    { OBJ := SQRT(SQR(quadKx) + SQR(quadKy) + SQR(MU0)); }
    CHROMA GAMMA CHROM_X CHROM_Y; {computes betatron chromaticities}
    OBJ := SQRT(SQR(CHROM_X) + SQR(CHROM_Y));
    WRITE 6 'SF1, SF2, SD, EBE = '&ST(SF1)&' '&ST(SF2)&ST(SD)&' '&ST(E1);
    WRITE 6 'quadKx, quadKy= '&ST(quadKx)&' '&ST(quadKy);
    WRITE 6 'MU0 = '&ST(MU0);
    WRITE 6 'OBJ ='&ST(OBJ);
  ENDFIT 1E-5 1000 1 OBJ;

  SGF1 := SF1; SF2 := SF2; SGD := SD; EBE := E1;
ENDPROCEDURE; {OPTIMIZE}


PROCEDURE MAIN;
  VARIABLE WHERE 100; VARIABLE MRKR 100;
  VARIABLE RESTMASSAMU 1; VARIABLE RESTMASSMEV 1;
  VARIABLE EKIN 1; VARIABLE GAMMA 1; VARIABLE BETA 1;
  VARIABLE CHRX 100; VARIABLE CHRY 100;

  VARIABLE SGF1 1; VARIABLE SGF2 1; VARIABLE SGD 1; VARIABLE EBE 1;
  VARIABLE OPTFLAG 10; {determines whether to use the optimized sextupole gradients}

  OPTFLAG := 'yes';

  {lattice parameters}
  IF OPTFLAG='yes'; {optimized sextupole grad values}
     SGF1 := 0.0475358; SGF2 := SGF1; SGD := -0.0124713;
     OPTFLAG := '-optSGxy';
  ELSEIF LO(1); {original values}
     SGF1 := 0.4400000; SGF2 := SGF1; SGD := -0.8199998;
     OPTFLAG := '';
  ENDIF;
  EBE := 100;

  
  MRKR := 'CHROM'&OPTFLAG;
  WRITE 6 'CHROM test' OPTFLAG; WRITE 6 'MARKER:' MRKR;
  DIRSET_AUTO 0;
  DIRSET WHERE 'data/BYPASS_SEX_CLEAR/CHROM/';
  GROUTF 'img/dump/TR' 1;
  
  OV 3 2 1; { FR 3; } {DAEPS 1E-12;} {this sets DA garbage collection tolerance}
  GAMMA := 1.1279235;
  RESTMASSAMU := 2.01410; RESTMASSMEV := RESTMASSAMU*AMUMEV;
  BETA := SQRT(GAMMA*GAMMA - 1)/GAMMA;
  EKIN := (GAMMA-1)*RESTMASSMEV;
  RP EKIN*PARA(1) RESTMASSAMU 1;
  RPS 1 -.142987;
  WRITE 6 '++++++++++ SETTING UP FOR DEUTERONS:';
  WRITE 6 'EKIN [MeV]          GAMMA          BETA';
  WRITE 6 SF(EKIN, '(F15.7)')&SF(GAMMA, '(F15.7)')&SF(BETA, '(F15.7)');
  WRITE 6 '+++++++++++++++++++++++++++++++++++';
  { SET lattice parameters }
  LATTICE SGF1 SGF2 SGD EBE 0 1;
  OPENF 9292 WHERE&'LATTICE-PARAMETERS:'&MRKR 'REPLACE';
    WRITE 9292 '     SGF1      SGF2           SGD            EBE  ';
    WRITE 9292 SF(SGF1, '(F15.7)')&SF(SGF2, '(F15.7)')&SF(SGD, '(F15.7)')&SF(EBE, '(F15.7)');
    CLOSEF 9292;
  CHROMA GAMMA CHRX CHRY;
    OPENF 3362 WHERE&'BTUNE_X:'&MRKR&'.da' 'REPLACE';
      WRITE 3362 CHRX; CLOSEF 3362;
    OPENF 3362 WHERE&'BTUNE_Y:'&MRKR&'.da' 'REPLACE';
      WRITE 3362 CHRY; CLOSEF 3362;
  WRITE 6 'LATTICE LENGTH' SPOS;

  OPTIMIZE SGF1 SGF2 SGD EBE GAMMA;
  OPENF 9292 WHERE&'LATTICE-PARAMETERS:CHROM-OPT'&MRKR 'REPLACE';
    WRITE 9292 '     SGF1      SGF2           SGD            EBE  ';
    WRITE 9292 SF(SGF1, '(F15.7)')&SF(SGF2, '(F15.7)')&SF(SGD, '(F15.7)')&SF(EBE, '(F15.7)');
    CLOSEF 9292;

ENDPROCEDURE; {MAIN}


PROCEDURE RUN;
  MAIN;
ENDPROCEDURE;
RUN; END;
