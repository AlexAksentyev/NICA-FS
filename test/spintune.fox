INCLUDE 'bin/setups/BENDS24/SEQFULL';

PROCEDURE INJECT NUM SX SY SZ;
  VARIABLE X 100; VARIABLE I 1;
  CR;
  X := LINSPACE(-5E-3, 5E-3, NUM);
  SR 0 0 0 0 0 0 0 0 1; SSR 0 0 1;
  LOOP I 1 NUM;
    SR X|I 0 0 0 0 0 0 0 1; SSR SX SY SZ;
    SR 0 0 X|I 0 0 0 0 0 1; SSR SX SY SZ;
    SR 0 0 0 0 0 (X|I)/10 0 0 1; SSR SX SY SZ;
  ENDLOOP;
ENDPROCEDURE;

PROCEDURE SPINTUNE WHERE OEID KZ1 KZ2 NTURN;
  {lattice parameters}
  VARIABLE QF  1; VARIABLE QD  1;
  VARIABLE HZ1 1; VARIABLE HZ2 1;
  VARIABLE MAPPARAMS 1 6; {HOLDS THE ABOVE VALUES}
  VARIABLE MAPARR 1000 6 472; VARIABLE SPNRARR 1000 3 3 472; {element map arrays}
  {variables required by TSS, GET_TUNE_ENSEMBLE}
  VARIABLE MU 800; VARIABLE NBAR 800 3; VARIABLE MUARR 800 4;
  {RF parameters}
  VARIABLE VRF 1 1 1;
  VARIABLE FREQ 1;
  VARIABLE HNUM 1;

  VARIABLE EL 1; {index of the current starting element}
  VARIABLE EL0 1; VARIABLE EL1 1; {select lattice segment from EL0 to EL1 for test}
  
  PROCEDURE PICK INDEX;
    VARIABLE I 1; VARIABLE J 1;
    LOOP I 1 TWOND; MSC(I) := MAPARR(I, INDEX); ENDLOOP;
    LOOP I 1 3; LOOP J 1 3; SSCR(I,J) := SPNRARR(I,J,INDEX) + 0*DD(1);
    ENDLOOP;ENDLOOP;
  ENDPROCEDURE;
  PROCEDURE MAKEMAP FROM TO START; {lattice segment FROM index TO index, beginning at START index}
    VARIABLE J 1;
    WRITE 6 '* '&ST(START);
    LMAPS START MAPARR SPNRARR; {sets the MAP, SPNR transfer maps to those of the i-th element}
    LOOP J (START+1) TO; WRITE 6 '**       '&ST(J);
      PICK J; LOCSET 0 0 0 0 0 0; UPDATE 1 1;
      ENDLOOP;
    LOOP J FROM (START-1); WRITE 6 '**       '&ST(J);
      PICK J; LOCSET 0 0 0 0 0 0; UPDATE 1 1;
      ENDLOOP;
  ENDPROCEDURE;
  PROCEDURE WST ELN OU;
    VARIABLE I 1; VARIABLE J 1; VARIABLE STR 100;
    LOOP J 1 NRAY ; STR := SF(ELN, '(I6)')&' '&SF(J-1,'(I6)') ;
      LOOP I 1 4 ; STR := STR&SF((MUARR(I)|J),'(E15.7)') ; ENDLOOP ;
      WRITE OU STR ;
    ENDLOOP ;
  ENDPROCEDURE;

  OV 3 3 0; SET_FOR_DEUTERONS HZ1 HZ2 QF QD;
  HZ1 := SNAKE_STR(G0); HZ2 := HZ1;

  MAPPARAMS(1) := HZ1; MAPPARAMS(2) := HZ2;
  MAPPARAMS(3) := KZ1; MAPPARAMS(4) := KZ2;
  MAPPARAMS(5) := QF;  MAPPARAMS(6) := QD;


  LATTICE MAPPARAMS MAPARR SPNRARR; {computes element matrices}

  {SETTING RF PARAMETERS}
  HNUM := 66;
  VRF(1, 1) := 100/HNUM; {RF Voltage [kV]}
  FREQ := HNUM*REVFREQ(ACCLEN(1)); {RF Frequency}
  UM; RF VRF 0 FREQ 0 0.05;
    INSERT 1 MAPARR SPNRARR 471;

  EL0 := 1; EL1 := 472;
  OPENF 3618 WHERE&'MU.dat' 'REPLACE';
  LOOP EL EL0 EL1;
    UM; CR;
    MAKEMAP EL0 EL1 EL; { OPENF 935 WHERE&'MAP'&MRK(EL) 'REPLACE'; PM 935; CLOSEF 935; }
    TSS MU NBAR 0;
    MUARR(1) := MU; MUARR(2) := NBAR(1); MUARR(3) := NBAR(2); MUARR(4) := NBAR(3);
    INJECT 30 0 0 1; GET_TUNE_ENSEMBLE MUARR;
    WST EL 3618;
  ENDLOOP;
  CLOSEF 3618;

  INJECT 30 0 0 1;
  OPENF 7 WHERE&'TRPRAY.dat' 'REPLACE';
  OPENF 8 WHERE&'TRPSPI.dat' 'REPLACE';
  TREL MAPARR SPNRARR 1 472 NTURN 7 8 OEID;
  CLOSEF 7; CLOSEF 8;
ENDPROCEDURE; {SPINTUNE}

PROCEDURE RUN;
  VARIABLE WHERE 100; VARIABLE COMMON 100;
  VARIABLE OEID 10; VARIABLE NTURN 1;

  OEID := 1&236&472; {outpud data only after these elements}
  NTURN := 50000;
  COMMON := 'data/SPINTUNE/50kTURNS/';
  DIRSET WHERE COMMON&'NO_NAVIG/';
  GROUTF 'img/dump/TR' 1;
  SPINTUNE WHERE OEID 0 0 NTURN;
  { DIRSET WHERE COMMON&'NAVIG-PSI30p/'; }
  { GROUTF 'img/dump/TR' 1; }
  { SPINTUNE WHERE .0004519 -0.07155497; }
  { DIRSET WHERE COMMON&'NAVIG-PSI90n/'; }
  { GROUTF 'img/dump/TR' 1; }
  { SPINTUNE WHERE 0 0.143109947; }
ENDPROCEDURE;
RUN; END;
