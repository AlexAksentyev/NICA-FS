INCLUDE 'bin/setups/BYPASS/FULL_SEX_CLEAR';

PROCEDURE INJECT NUM PSI_DEG; {at injection: SX = 0, SY, SZ = sin PSI, cos PSI}
  VARIABLE X 100; VARIABLE I 1;
  VARIABLE PSI 1; VARIABLE SY 1; VARIABLE SZ 1;
  PSI := DEG2RAD(PSI_DEG); {PSI IN RADIANS}
  X := LINSPACE(-1E-3, 1E-3, NUM);
  SY := SIN(PSI); SZ := COS(PSI);
  SR 0 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
  LOOP I 1 NUM;
    SR X|I 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
    SR 0 0 X|I 0 0 0 0 0 1; SSR 0 SY SZ;
    SR 0 0 0 0 0 (X|I)/10 0 0 1; SSR 0 SY SZ;
  ENDLOOP;
ENDPROCEDURE;

PROCEDURE OPTIMIZE SEXTGF1 SEXTGF2 SEXTGD {arguments} EBE {parameter};
                                           {optimizes the sextupoles with spin-tune quadratic coefficients as target}
  VARIABLE MU 800; VARIABLE NBAR 800 3;  {where the coefficients are taken from}
  {optimized variables}
  VARIABLE SGF1 1; VARIABLE SGF2 1;  { at this point I * DON'T * distinguish }
  VARIABLE SGD 1;
  VARIABLE E1 1;                     {stays * FIXED * }
  {target}      
  VARIABLE quadKx 1; VARIABLE quadKy 1; VARIABLE quadKd 1; VARIABLE MU0 1;
  VARIABLE OBJ 1;

  SGF1 := SEXTGF1; SGF2 := SEXTGF2; SGD := SEXTGD; E1 := EBE; {set initial values for optimization}
  FIT SGF1 SGF2 SGD;
    { SGF2 := SGF1; }
    LATTICE SGF1 SGF2 SGD E1 0 1; {optimize assuming EBE=100 kV/cm (BBE[=0] irrelevant b/c R1=R2 in WIEN)}
    TSS MU NBAR 0;
      MU0 := CONS(MU);
      DAPEE MU 11 quadKx;
      DAPEE MU 33 quadKy;
      DAPEE MU 66 quadKd;
    OBJ := SQRT(SQR(quadKx) + SQR(quadKy) + SQR(quadKd));
    WRITE 6 'SGF, SGD, EBE = '&ST(SGF1)&' '&ST(SGD)&' '&ST(E1);
    WRITE 6 'Kx^2, Ky^2, Kd^2= '&ST(quadKx)&' '&ST(quadKy)&ST(quadKd);
    WRITE 6 'MU0 = '&ST(MU0);
    WRITE 6 'OBJ ='&ST(OBJ);
  ENDFIT 1E-5 1000 1 OBJ;

  SEXTGF1 := SGF1; SEXTGF2 := SGF2; SEXTGD := SGD; EBE := E1;
ENDPROCEDURE; {OPTIMIZE}

PROCEDURE PARAMETER_OUTPUT DIR TAG COMMENT SF1 SF2 SD EBE;
  OPENF 9292 DIR&'LATTICE-PARAMETERS:'&TAG&'.txt' 'REPLACE';
    WRITE 9292 '# '&COMMENT;
    WRITE 9292 '#    SF1       SF2            SD             EBE';
    WRITE 9292 SF(SF1, '(F15.7)')&SF(SF2, '(F15.7)')&SF(SD, '(F15.7)')&SF(EBE, '(F15.7)');
    CLOSEF 9292;
ENDPROCEDURE;

PROCEDURE TSS_OUTPUT DIR TAG MU NBAR;
  VARIABLE I 1;
  OPENF 8294 DIR&'NU:'&TAG&'.da' 'REPLACE'; WRITE 8294 MU; CLOSEF 8294;
  LOOP I 1 3;
    OPENF 8294 DIR&'NBAR'&MRK(I)&':'&TAG&'.da' 'REPLACE';
    WRITE 8294 NBAR(I);
    CLOSEF 8294;ENDLOOP;
ENDPROCEDURE;

PROCEDURE ANALYSIS DIR TAG {output loc} COMMENT SGF1 SGF2 SGD EBE {lattice pars};
  {TSS}
  VARIABLE MU 800; VARIABLE NBAR 800 3;
  {tracking}
  VARIABLE PNUM 1;
  VARIABLE PSI0_DEG 1;
  VARIABLE NTURN 1;
  
  NTURN := 300000;
  PSI0_DEG := 45;
  PNUM := 10;

  { *** analysis *** }
  LATTICE SGF1 SGF2 SGD EBE 0.0 {ignored} 1;
    {print transfer map}
     OPENF 636 DIR&'ORBITAL:'&TAG&'.tm' 'REPLACE';
     PM 636; CLOSEF 636;
     {print spin transfer map}
     OPENF 636 DIR&'SPIN:'&TAG&'.tm' 'REPLACE';
  TSS MU NBAR 0; TSS_OUTPUT DIR TAG MU NBAR;
  PARAMETER_OUTPUT DIR TAG COMMENT SGF1 SGF2 SGD EBE;
  INJECT PNUM PSI0_DEG;
  WRITE 6 '******************** STARTING TRACKING';
  OPENF 99 DIR&'PRAY:'&TAG&'.dat' 'REPLACE';
    PRAY 99; CLOSEF 99;
  OPENF 772 DIR&'TRPRAY:'&TAG&'.dat' 'REPLACE';
  OPENF 893 DIR&'TRPSPI:'&TAG&'.dat' 'REPLACE';
    TRPRAY 772; TRPSPI 893;
    TR NTURN NINT(NTURN/5000) -1 -3 1.2 1.2 0 0 -12;
    CLOSEF 772; CLOSEF 893;
ENDPROCEDURE;

PROCEDURE MAIN;
  VARIABLE WHERE 100; {to locate data files}
  VARIABLE GAMMA 1;
  {optimization}
  VARIABLE SGF1 1; VARIABLE SGF2 1; VARIABLE SGD 1;
  VARIABLE EBE 1; {fixed}
  

  DIRSET WHERE 'data/BYPASS_SEX_CLEAR/optimize-SEXT/All-3/';
  GROUTF 'img/dump/TR' 1;
  GAMMA := 1.1279235; {240 MeV for deuterons}
  {initial values for optimization}
  SGF1 := 0.4400000434*0;  { default }
  SGF2 := SGF1;
  SGD := -0.8199998015*0; { values }
  EBE := 99.1475; { optimize around FS EBE }
  
  
  OV 5 3 0;
  DAEPS 1E-12; {this sets DA garbage collection tolerance so that the TRANSFER MAP doesn't get the 1e-15 ABERRATION coefficient}
  SET_FOR_DEUTERONS GAMMA; { SET lattice parameters }

  {*** PRE-OPT TRACKING *** }
  ANALYSIS WHERE 'pre-opt' 'lattice parameters prior to optimization (of Kx^2, Ky^2)' SGF1 SGF2 SGD EBE;

  { *** OPTIMIZATION *** }
  OPTIMIZE SGF1 SGF2 SGD {arguments} EBE {parameter};
  WRITE 6 'USE SF1, SF2, SD = '&ST(SGF1)&', '&ST(SGF2)&', '&ST(SGD);

  {*** POST-OPT TRACKING *** }
  ANALYSIS WHERE 'post-opt' 'lattice parameters after to optimization (of Kx^2, Ky^2)' SGF1 SGF2 SGD EBE;

ENDPROCEDURE; {MAIN}

PROCEDURE RUN;
  MAIN;
ENDPROCEDURE;
RUN; END;
