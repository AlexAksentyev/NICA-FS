INCLUDE 'bin/setups/SECOND-ST/FULL';

PROCEDURE INJECT NUM PSI_DEG; {at injection: SX = 0, SY, SZ = sin PSI, cos PSI}
  VARIABLE X 100; VARIABLE I 1;
  VARIABLE PSI 1; VARIABLE SY 1; VARIABLE SZ 1;
  PSI := DEG2RAD(PSI_DEG); {PSI IN RADIANS}
  X := LINSPACE(-3E-3, 3E-3, NUM);
  SY := SIN(PSI); SZ := COS(PSI);
  CR;
  SR 0 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
  LOOP I 1 NUM;
    SR X|I 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
    SR 0 0 X|I 0 0 0 0 0 1; SSR 0 SY SZ;
    SR 0 0 0 0 0 (X|I)/10 0 0 1; SSR 0 SY SZ;
  ENDLOOP;
ENDPROCEDURE;

                {(1,2,3)        (1,2,3,4)    (1)  }
PROCEDURE MAIN CASEPARS        LATPARS     DATDIR;
                {psi: initial,  X [2st],    folder_name,
                      final,    XS [2st],
                      rate}
  {data-files}
  VARIABLE WHERE 100; VARIABLE MRKR 100;
  {tracking}
  VARIABLE NAVIPSI_DI 1; VARIABLE NAVIPSI_DF 1; {navi-psi change range}
  VARIABLE NSWITCH 1; {number of navigator-psi switchings}
  VARIABLE CASE 1; {current switch case} VARIABLE PCT 1; {tracking percent}
  {lattice}
  VARIABLE X  1 2; VARIABLE XS 1 2;
  {navigators}
  VARIABLE NAVIPSI_DEG 1; {navi-set psi}
  VARIABLE NAVINU 1; {navigator-set spin tune}    {this will slightly vary (~3%) with the NAVIPSI_DEG}
  {particles}
  VARIABLE PNUM 1; {number of particles per bunch}
  VARIABLE PSI0_DEG 1; {initial SPIN tilt angle}
  VARIABLE RAYH 1000 8; VARIABLE SPIH 1000 3; {keeps RAY, SPI data after each turn}
  {^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^}
  PROCEDURE PUSH NAVIPSI_DEG SOU ROU TOU;
    VARIABLE KS 1 2; {navigator solenoid strengths}
    VARIABLE MUARR 100 4; {holds NU, NX, NY, NZ of the ray ensemble}
    VARIABLE FTR 1; {number of turns at a constant psi}
    VARIABLE I 1; VARIABLE SCALE 1; {scale needed to keep data below a certain size}
    VARIABLE NUCORR 1; {corrected navi-nu accounting for the second-order effect (see eq (2.2.1) in report)}
    FTR := 10000;
    SCALE := INT(NSWITCH*FTR/5000);
    NUCORR := NAVINU - 4E-4*(ABS(COS(DEG2RAD(NAVIPSI_DEG))) - 1);   {remove the 2nd order effect; see eq (2.2.1) in report}
    NAVIGATORS NUCORR NAVIPSI_DEG KS; {recompute solenoid strengths}
    LATTICE X XS KS 1;                {recompute lattice}
    LRAY RAYH; LSPI SPIH;             {loads RAY, SPI data after clearing}
    GET_TUNE_ENSEMBLE MUARR;          {compute nu, nbar for the current turn}

    LOOP I 1 (FTR/SCALE);
      TR FTR -SCALE -1 -3 1.1 1.1 0 0 -12;
      WRITETBL SOU SPI   3 SF(1+FTR*CASE+I*SCALE, '(I15)');
      WRITETBL ROU RAY   6 SF(1+FTR*CASE+I*SCALE, '(I15)');
      WRITETBL TOU MUARR 4 SF(1+FTR*CASE+I*SCALE, '(I15)');
    ENDLOOP;
  ENDPROCEDURE; {PUSH}
  {^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^}
  PNUM := 10; {x 3 bunches + 1 (reference ray)}
  NAVINU := 1.5E-2; {minimum (and maximum) stable spin tune for PROTONS}
  NAVIPSI_DI := CASEPARS(1);
  NAVIPSI_DF := CASEPARS(2);
  X(1)  := LATPARS(1); X(2)  := LATPARS(2);
  XS(1) := LATPARS(3); XS(2) := LATPARS(4);
  NSWITCH := 1;
  DIRSET WHERE DATDIR;
  
  {inject particles}
  PSI0_DEG := 180 - NAVIPSI_DI;    {align inital spin with the polarization axis for 100% initial polarization}
  INJECT PNUM PSI0_DEG; SRAY RAYH; SSPI SPIH; {saves RAY, SPI against CR}
  MRKR := 'PSI0spin=PSInavi';
  WRITE 6 '^^^^ INITIAL SPIN PSI'&SF(PSI0_DEG,'(F15.7)');
  
  {++++++++++ PREPARING FILES FOR DATA OUTPUT ++++++++++}
  OPENF 100501 FILENAME(WHERE, 'TRPSPI', MRKR) 'REPLACE';
  WRITE 100501 '# number of rays:   '&SI(PNUM*3+2);      {cf. INJECT procedure (+1 for COSY's default 0-ray)}
  WRITE 100501 '# TURN PID S_X S_Y S_Z';
  WRITETBL 100501 SPI 3 SF(0, '(I15)');
  OPENF 100502 FILENAME(WHERE, 'TRPRAY', MRKR) 'REPLACE';
  WRITE 100502 '# number of rays:   '&SI(PNUM*3+2);
  WRITE 100502 '# TURN PID X A Y B T D';
  WRITETBL 100502 RAY 6 SF(0, '(I15)');
  OPENF 100503 FILENAME(WHERE, 'TRMUARR', MRKR) 'REPLACE';
  WRITE 100503 '# number of rays:   '&SI(PNUM*3+2);
  WRITE 100503 '# TURN PID NU NX NY NZ';
  {+++++++++++++++++++++++++++++++++++++++++++++++++++++}

  {tracking}
  LOOP CASE 0 NSWITCH; NAVIPSI_DEG := NAVIPSI_DI + CASE*CASEPARS(3); {CASEPARS(3) (NAVIPSI in RUN) = [deg/turn]}
    PUSH NAVIPSI_DEG 100501 100502 100503;  {push through one turn}
      SRAY RAYH; SSPI SPIH; {saves RAY, SPI against CR}
    PCT := 100*CASE/NSWITCH;
      IF MOD(PCT, 10)=0; WRITE 6 '                                                      '&SI(PCT)&' %'; ENDIF;
  ENDLOOP;
ENDPROCEDURE; {MAIN}

PROCEDURE RUN;
  VARIABLE DATDIR 100; {where to output data files}
  VARIABLE TRIALV 1; VARIABLE TRIALP 1; {iterator* over DEGperSWITCH}
  VARIABLE NAVIPSI 1 3; {iteration range* & iterated navi psi rate-of-change* [deg/switch]}
  {LATTICE ARGUMENTS}
  VARIABLE EKIN 1;
  VARIABLE X  1 2; VARIABLE XS 1 2;
  VARIABLE LATPARS 1 4; {[flat] array (X, XS)}
  {********** CONTROLS **********}
  EKIN := 130; {injection kinetic energy in MeV}
  NAVIPSI(1) := 100; {initial ...}
  NAVIPSI(2) := 180; {... final psi [deg]}
  {NAVIPSI(3)    set     below                       ($$)}
  {*** NOTE ***: I control NAVINU in MAIN:PUSH (above) --------------------------  at the moment}
  {******************************}
  GROUTF 'img/dump/TR' 1;
  DIRSET_AUTO 1;
  OV 3 3 0;
  TRR 1; {turn on RESUME MODE}
  
  { SET lattice parameters }
  SET_FOR_PROTONS EKIN X XS;
  LATPARS(1) := X(1);  LATPARS(2) := X(2);
  LATPARS(3) := XS(1); LATPARS(4) := XS(2);
  
  LOOP TRIALP 0 0;
    LOOP TRIALV 1 2; IF TRIALV=1; TRIALV:=45; ELSEIF TRIALV=2; TRIALV:=90; ENDIF;
      NAVIPSI(3) := TRIALV * 10^(TRIALP);  {[deg/switch] rate-of-change*}      {($$)}
      DATDIR := 'data/SECOND-ST/'&MRK(EKIN)&'MeV/NAVI-VARI-1switch/RATE_'&MRK(TRIALV)&'/';{E'&MRK(TRIALP)&'/';}
      MAIN NAVIPSI LATPARS DATDIR;
    ENDLOOP; {TRIALV}
  ENDLOOP; {TRIALP}

ENDPROCEDURE; {RUN}
RUN; END;
