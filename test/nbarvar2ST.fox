INCLUDE 'bin/setups/SECOND-ST/FULL';

PROCEDURE INJECT NUM PSI_DEG; {at injection: SX = 0, SY, SZ = sin PSI, cos PSI}
  VARIABLE X 100; VARIABLE I 1;
  VARIABLE PSI 1; VARIABLE SY 1; VARIABLE SZ 1;
  PSI := DEG2RAD(PSI_DEG); {PSI IN RADIANS}
  X := LINSPACE(-3E-3, 3E-3, NUM);
  SY := SIN(PSI); SZ := COS(PSI);
  CR;
  SR 0 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
  LOOP I 1 NUM;
    SR X|I 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
    SR 0 0 X|I 0 0 0 0 0 1; SSR 0 SY SZ;
    SR 0 0 0 0 0 (X|I)/10 0 0 1; SSR 0 SY SZ;
  ENDLOOP;
ENDPROCEDURE;

PROCEDURE RUN;
  VARIABLE WHERE 100; VARIABLE MRKR 100;
  VARIABLE RAYH 1000 8; VARIABLE SPIH 1000 3; {keeps RAY, SPI data after each turn}
  VARIABLE NTURN 1; {number of turns to track for}
  VARIABLE DEGperTURN 1; {navi psi rate-of-change [deg/turn]}
  VARIABLE TURNperDEG 1; {number of turns per constant psi (reverse of previous variable)}
  VARIABLE STEP 1; {current step} VARIABLE PCT 1; {tracking percent}
  VARIABLE NAVIPSI_DI 1; VARIABLE NAVIPSI_DF 1; {INITIAL AND FINAL NBAR TILTS}
  VARIABLE NAVINU 1; {navigator-set spin tune} VARIABLE NAVIPSI_DEG 1; {navi-set psi}
  VARIABLE PSI0_DEG 1; {initial SPIN tilt angle}
  VARIABLE PNUM 1; {number of particles per bunch}
  {LATTICE ARGUMENTS}
  VARIABLE X  1 2; VARIABLE XS 1 2;
  VARIABLE EKIN 1;
  PROCEDURE PUSH NAVIPSI_DEG SOU ROU TOU;
    VARIABLE KS 1 2; {NAVIGATOR SOLENOID STRENGTHS}
    VARIABLE MUARR 100 4; {holds NU, NX, NY, NZ of the ray ensemble}
    NAVIGATORS NAVINU NAVIPSI_DEG KS; {recompute solenoid strengths}
    LATTICE X XS KS 1;                {recompute lattice}
    LRAY RAYH; LSPI SPIH;             {loads RAY, SPI data after clearing}
    GET_TUNE_ENSEMBLE MUARR;          {compute nu, nbar for the current turn}

    TR 1 -1 -1 -3 1.1 1.1 0 0 -12;
    WRITETBL SOU SPI   3 SF(STEP+1, '(I15)');
    WRITETBL ROU RAY   6 SF(STEP+1, '(I15)');
    WRITETBL TOU MUARR 4 SF(STEP+1, '(I15)');
  ENDPROCEDURE; {PUSH}
  {********** CONTROLS **********}
  PNUM := 10; {x 3 bunches + 1 (reference ray)}
  EKIN := 130; {injection kinetic energy in MeV}
  NAVINU := 1.5e-2;
  NAVIPSI_DI := 100;
  NAVIPSI_DF := 180;
  DEGperTURN := 1;
  {******************************}
  TURNperDEG := 1/DEGperTURN;
  NTURN := (NAVIPSI_DF - NAVIPSI_DI) * TURNperDEG;
  MRKR := 'PSI0spin=PSInavi'; {marker to identify data files}
  GROUTF 'img/dump/TR' 1;
  DIRSET_AUTO 1;
  OV 3 3 0;
  TRR 1; {turn on RESUME MODE}
  
  { SET lattice parameters }
  SET_FOR_PROTONS EKIN X XS;
  {inject particles}
  PSI0_DEG := 180 - NAVIPSI_DI;    {align inital spin with the polarization axis for 100% initial polarization}
  INJECT PNUM PSI0_DEG;
  WRITE 6 '^^^^ INITIAL SPIN PSI'&SF(PSI0_DEG,'(F15.7)');
  DIRSET WHERE 'data/SECOND-ST/'&MRK(EKIN)&'MeV/NAVI-VARI/'&MRK(NTURN)&'/';
  
  {++++++++++ PREPARING FILES FOR DATA OUTPUT ++++++++++}
  OPENF 100501 FILENAME(WHERE, 'TRPSPI', MRKR) 'REPLACE';
  WRITE 100501 '# number of rays:   '&SI(PNUM*3+2);      {cf. INJECT procedure (+1 for COSY's default 0-ray)}
  WRITE 100501 '# TURN PID S_X S_Y S_Z';
  WRITETBL 100501 SPI 3 SF(0, '(I15)');
  OPENF 100502 FILENAME(WHERE, 'TRPRAY', MRKR) 'REPLACE';
  WRITE 100502 '# number of rays:   '&SI(PNUM*3+2);
  WRITE 100502 '# TURN PID X A Y B T D';
  WRITETBL 100502 RAY 6 SF(0, '(I15)');
  OPENF 100503 FILENAME(WHERE, 'TRMUARR', MRKR) 'REPLACE';
  WRITE 100503 '# number of rays:   '&SI(PNUM*3+2);
  WRITE 100503 '# TURN PID NU NX NY NZ';
  {+++++++++++++++++++++++++++++++++++++++++++++++++++++}
  SRAY RAYH; SSPI SPIH; {saves RAY, SPI against CR}
  
  LOOP STEP 0 NTURN; NAVIPSI_DEG := NAVIPSI_DI + STEP*DEGperTURN;
    PUSH NAVIPSI_DEG 100501 100502 100503;
      SRAY RAYH; SSPI SPIH; {saves RAY, SPI against CR}
    PCT := 100*STEP/NTURN;
      IF MOD(PCT, 10)=0; WRITE 6 '                         '&SI(PCT)&' %'; ENDIF;
  ENDLOOP;
ENDPROCEDURE; {RUN}
RUN; END;
