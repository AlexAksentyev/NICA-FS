INCLUDE 'bin/setups/BYPASS/FULL_SEX_CLEAR';

PROCEDURE INJECT NUM PSI_DEG; {at injection: SX = 0, SY, SZ = sin PSI, cos PSI}
  VARIABLE X 100; VARIABLE I 1;
  VARIABLE PSI 1; VARIABLE SY 1; VARIABLE SZ 1;
  PSI := DEG2RAD(PSI_DEG); {PSI IN RADIANS}
  X := LINSPACE(-1E-3, 1E-3, NUM);
  SY := SIN(PSI); SZ := COS(PSI);
  SR 0 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
  LOOP I 1 NUM;
    SR X|I 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
    SR 0 0 X|I 0 0 0 0 0 1; SSR 0 SY SZ;
    SR 0 0 0 0 0 (X|I)/10 0 0 1; SSR 0 SY SZ;
  ENDLOOP;
ENDPROCEDURE;

PROCEDURE OPTIMIZE_old EBE BBE;
  VARIABLE OBJ 1; VARIABLE EB1 1; {optimized value of the EB element's electric field}
                   VARIABLE EB2 1; {magnetic field}
  VARIABLE SXX 1; VARIABLE SYY 1; VARIABLE SZZ 1; {Spin transfer map's coefficients S11, S22, S33}
  VARIABLE SXZ 1;

  EB1 := EBE; EB2 := BBE;
  FIT EB1 EB2;
    LATTICE EB1 EB2 1;
    SXX := CONS(SPNR(1,1)); SXZ := CONS(SPNR(1,3));
    SYY := CONS(SPNR(2,2)); SZZ := CONS(SPNR(3,3));
    WRITE 6 'SXX = '&ST(SXX);
    WRITE 6 'SXZ = '&ST(SXZ);
    WRITE 6 'SZZ = '&ST(SZZ);
    OBJ := SQRT(SQR(SXX-1) + SQR(SXZ));
    WRITE 6 'EBE, BBE = '&ST(EB1)&', '&ST(EB2);
    WRITE 6 'OBJ ='&ST(OBJ);
  ENDFIT 1E-12 1000 1 OBJ;

  EBE := EB1; BBE := EB2;
ENDPROCEDURE; {OPTIMIZE old (via Spin-Transfer map}

PROCEDURE OPTIMIZE EBE {argument} SF1 SF2 SD {parameters}; { optimizes the Wien-filter's * E-field * }
                                                            { with spin-tune constant MU0 as target }
  VARIABLE MU 800; VARIABLE NBAR 800 3; 
  {optimized variables}
  VARIABLE E1 1; { optimized value of the EB element's electric field }
                                                      { magnetic field is ignored # b/c in WIEN R2=R1 }
  {target}
  VARIABLE OBJ 1; VARIABLE MU0 1;
  VARIABLE EB1 1; 

  EB1 := EBE;
  FIT EB1;
    LATTICE SF1 SF2 SD EB1 0.0 {BBE ignored # } 1;
    TSS MU NBAR 0;
      MU0 := CONS(MU);
      OBJ := ABS(MU0);
    WRITE 6 'EBE, MU0 = '&ST(EB1)&', '&ST(MU0);
    WRITE 6 'OBJ ='&ST(OBJ);
  ENDFIT 1E-7 1000 1 OBJ;

  EBE := EB1;
ENDPROCEDURE; {OPTIMIZE (via TSS)}

PROCEDURE PARAMETER_OUTPUT DIR TAG COMMENT SF1 SF2 SD EBE;
  OPENF 9292 DIR&'LATTICE-PARAMETERS:'&TAG&'.txt' 'REPLACE';
    WRITE 9292 '# '&COMMENT;
    WRITE 9292 '#    SF1       SF2            SD             EBE';
    WRITE 9292 SF(SF1, '(F15.7)')&SF(SF2, '(F15.7)')&SF(SD, '(F15.7)')&SF(EBE, '(F15.7)');
    CLOSEF 9292;
ENDPROCEDURE;

PROCEDURE MAIN;
  VARIABLE WHERE 100; VARIABLE MRKR 50 2;
  {lattice parameters}
  VARIABLE GAMMA 1;
  VARIABLE SGF1 1; VARIABLE SGF2 1; {sextupole gradients}
  VARIABLE SGD 1;
  {optimization}
  VARIABLE EBE 1;
  {post-opt tracking}
  VARIABLE PNUM 1;
  VARIABLE PSI0_DEG 1;
  VARIABLE NTURN 1;

  MRKR(1) := 'optimize-EB';
  DIRSET WHERE 'data/BYPASS_SEX_CLEAR/';
  GROUTF 'img/dump/TR' 1;
  GAMMA := 1.1279235; {240 MeV for deuterons}
  {optimization}
  EBE := 100;     { <- target: search for optimum around 100 kV/cm }
  SGF1 := 0.4400000434*0; SGF2 := SGF1; {parameters}
  SGD := -0.8199998015*0; {default sextupole grad values}
  {tracking}
  NTURN := 30000;
  PSI0_DEG := 45;
  PNUM := 10;
  
  
  OV 3 3 0;
  DAEPS 1E-12; {this sets DA garbage collection tolerance so that the TRANSFER MAP doesn't get the 1e-15 ABERRATION coefficient}
  SET_FOR_DEUTERONS GAMMA; { SET lattice parameters }

  { *** PRE-OPT TRACKING *** }
  MRKR(2) := 'pre-'&MRKR(1);
  LATTICE SGF1 SGF2 SGD EBE 0.0 {ignored} 1;
    {print transfer map}
     OPENF 636 WHERE&'ORBITAL:'&MRKR(2)&'.tm' 'REPLACE';
     PM 636; CLOSEF 636;
     {print spin transfer map}
     OPENF 636 WHERE&'SPIN:'&MRKR(2)&'.tm' 'REPLACE';
  PARAMETER_OUTPUT WHERE MRKR(2) 'lattice parameters prior to optimization (of MU0)' SGF1 SGF2 SGD EBE;
  INJECT PNUM PSI0_DEG;
  WRITE 6 '******************** STARTING TRACKING';
  OPENF 99 WHERE&'PRAY:'&MRKR(2)&'.dat' 'REPLACE';
  PRAY 99; CLOSEF 99;
  OPENF 772 WHERE&'TRPRAY:'&MRKR(2)&'.dat' 'REPLACE';
  OPENF 893 WHERE&'TRPSPI:'&MRKR(2)&'.dat' 'REPLACE';
  TRPRAY 772; TRPSPI 893;
  TR NTURN NINT(NTURN/5000) -1 -3 1.2 1.2 0 0 -12;
  CLOSEF 772; CLOSEF 893;

  { *** OPTIMIZATION *** }
  OPTIMIZE EBE {argument} SGF1 SGF2 SGD {parameters};
  WRITE 6 'USE EBE = '&ST(EBE);
  

  { *** POST-OPT TRACKING *** }
  MRKR(2) := 'post-'&MRKR(1);
  LATTICE SGF1 SGF2 SGD EBE 0.0 {ignored} 1; { installing new Wien-filter E-field value }
   {print transfer map}
    OPENF 636 WHERE&'ORBITAL:'&MRKR(2)&'.tm' 'REPLACE';
    PM 636; CLOSEF 636;
    {print spin transfer map}
    OPENF 636 WHERE&'SPIN:'&MRKR(2)&'.tm' 'REPLACE';
    PSM 636; CLOSEF 636;
  PARAMETER_OUTPUT WHERE MRKR(2) 'lattice parameters after optimization (of MU0)' SGF1 SGF2 SGD EBE;
  INJECT PNUM PSI0_DEG;
  WRITE 6 '******************** STARTING TRACKING';
  OPENF 99 WHERE&'PRAY:'&MRKR(2)&'.dat' 'REPLACE';
  PRAY 99; CLOSEF 99;
  OPENF 772 WHERE&'TRPRAY:'&MRKR(2)&'.dat' 'REPLACE';
  OPENF 893 WHERE&'TRPSPI:'&MRKR(2)&'.dat' 'REPLACE';
  TRPRAY 772; TRPSPI 893;
  TR NTURN NINT(NTURN/5000) -1 -3 1.2 1.2 0 0 -12;
  CLOSEF 772; CLOSEF 893;

ENDPROCEDURE; {MAIN}

PROCEDURE RUN;
  MAIN;
ENDPROCEDURE;
RUN; END;
