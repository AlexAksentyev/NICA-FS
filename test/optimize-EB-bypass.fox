INCLUDE 'bin/setups/BYPASS/FULL';

PROCEDURE INJECT NUM PSI_DEG; {at injection: SX = 0, SY, SZ = sin PSI, cos PSI}
  VARIABLE X 100; VARIABLE I 1;
  VARIABLE PSI 1; VARIABLE SY 1; VARIABLE SZ 1;
  PSI := DEG2RAD(PSI_DEG); {PSI IN RADIANS}
  X := LINSPACE(-1E-3, 1E-3, NUM);
  SY := SIN(PSI); SZ := COS(PSI);
  SR 0 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
  LOOP I 1 NUM;
    SR X|I 0 0 0 0 0 0 0 1; SSR 0 SY SZ;
    SR 0 0 X|I 0 0 0 0 0 1; SSR 0 SY SZ;
    SR 0 0 0 0 0 (X|I)/10 0 0 1; SSR 0 SY SZ;
  ENDLOOP;
ENDPROCEDURE;

PROCEDURE OPTIMIZE_old EBE BBE;
  VARIABLE OBJ 1; VARIABLE EB1 1; {optimized value of the EB element's electric field}
                   VARIABLE EB2 1; {magnetic field}
  VARIABLE SXX 1; VARIABLE SYY 1; VARIABLE SZZ 1; {Spin transfer map's coefficients S11, S22, S33}
  VARIABLE SXZ 1;

  EB1 := EBE; EB2 := BBE;
  FIT EB1 EB2;
    LATTICE EB1 EB2 1;
    SXX := CONS(SPNR(1,1)); SXZ := CONS(SPNR(1,3));
    SYY := CONS(SPNR(2,2)); SZZ := CONS(SPNR(3,3));
    WRITE 6 'SXX = '&ST(SXX);
    WRITE 6 'SXZ = '&ST(SXZ);
    WRITE 6 'SZZ = '&ST(SZZ);
    OBJ := SQRT(SQR(SXX-1) + SQR(SXZ));
    WRITE 6 'EBE, BBE = '&ST(EB1)&', '&ST(EB2);
    WRITE 6 'OBJ ='&ST(OBJ);
  ENDFIT 1E-12 1000 1 OBJ;

  EBE := EB1; BBE := EB2;
ENDPROCEDURE; {OPTIMIZE old (via Spin-Transfer map}

PROCEDURE OPTIMIZE EBE;
  VARIABLE MU 800; VARIABLE NBAR 800 3; VARIABLE MU0 1;
  VARIABLE OBJ 1; VARIABLE EB1 1; {optimized value of the EB element's electric field}
                                       {magnetic field is ignneored b/c in WIEN R2=R1}
  VARIABLE SGx 1; VARIABLE SGy 1; {sextupole gradients}
  SGx := 0.4400000434; SGy := -0.8199998015; {default sextupole grad values}

  EB1 := EBE;
  FIT EB1;
    LATTICE SGx SGy EB1 1;
    TSS MU NBAR 0;
    MU0 := CONS(MU);
    OBJ := ABS(MU0);
    WRITE 6 'EBE, MU0 = '&ST(EB1)&', '&ST(MU0);
    WRITE 6 'OBJ ='&ST(OBJ);
  ENDFIT 1E-9 1000 1 OBJ;

  EBE := EB1;
ENDPROCEDURE; {OPTIMIZE (via TSS)}

PROCEDURE MAIN;
  VARIABLE WHERE 100; VARIABLE MRKR 50;
  VARIABLE GAMMA 1;
  {optimization}
  VARIABLE EBE 1; VARIABLE BBE 1; 
  {post-opt tracking}
  VARIABLE PNUM 1;
  VARIABLE PSI0_DEG 1;
  VARIABLE NTURN 1;
  VARIABLE SEXTGx 1; VARIABLE SEXTGy 1;

  MRKR := 'FULL-optEB';
  DIRSET WHERE 'data/BYPASS/';
  GROUTF 'img/dump/TR' 1;
  GAMMA := 1.129;
  EBE := 100;     {search for optimum around 100 kV/cm ...}
  BBE := 0.72975; {... and 0.72975 kGs}
  NTURN := 30000;
  PSI0_DEG := 0;
  PNUM := 10;

  SEXTGx := 0.4400000434; SEXTGy := -0.8199998015; {default sextupole grad values}
  
  
  OV 3 3 0;
  DAEPS 1E-12; {this sets DA garbage collection tolerance so that the TRANSFER MAP doesn't get the 1e-15 ABERRATION coefficient}
  { SET lattice parameters }
  SET_FOR_DEUTERONS GAMMA;
  
  OPTIMIZE EBE;
  WRITE 6 'USE EBE = '&ST(EBE);
  {print transfer map}
   OPENF 636 WHERE&'TrMAP:'&MRKR 'REPLACE';
   PM 636; CLOSEF 636;
   {print spin transfer map}
   OPENF 636 WHERE&'SpTrMAP:'&MRKR 'REPLACE';
   PSM 636; CLOSEF 636;


  {tracking with new values}
  LATTICE SEXTGx SEXTGy EBE 1;
  INJECT PNUM PSI0_DEG;
  WRITE 6 '******************** STARTING TRACKING';
  OPENF 99 WHERE&'PRAY:'&MRKR&'.dat' 'REPLACE';
  PRAY 99; CLOSEF 99;
  OPENF 772 WHERE&'TRPRAY:'&MRKR&'.dat' 'REPLACE';
  OPENF 893 WHERE&'TRPSPI:'&MRKR&'.dat' 'REPLACE';
  TRPRAY 772; TRPSPI 893;
  TR NTURN NINT(NTURN/5000) -1 -3 1.2 1.2 0 0 -12;
  CLOSEF 772; CLOSEF 893;

ENDPROCEDURE; {MAIN}

PROCEDURE RUN;
  MAIN;
ENDPROCEDURE;
RUN; END;
