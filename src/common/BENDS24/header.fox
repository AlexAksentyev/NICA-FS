INCLUDE 'bin/elements';

FUNCTION ACCLEN DUMMY;
  ACCLEN := 503;
ENDFUNCTION;

FUNCTION SNAKE_STR G;
  SNAKE_STR := 1/12 * PI/(1 + G)*1/.7;
ENDFUNCTION;

PROCEDURE SET_FOR_PROTONS HZ1 HZ2 QF QD;
  VARIABLE EKIN 1; VARIABLE GAMMA 1; VARIABLE RESTMASSAMU 1;
  GAMMA := 1.14;
  RESTMASSAMU := 1.00728; EKIN := (GAMMA-1)*RESTMASSAMU*AMUMEV;
  RP EKIN RESTMASSAMU 1;
  RPS 1 1.792847;
  HZ1 := 0.1339129939629264; {optimized value but didn't ckeck how long FS holds} { SNAKE_STR(G0); }
  HZ2 := HZ1;
  QF  := -0.011;
  QD  := -0.012;
  WRITE 6 '++++++++++ SETTING UP FOR PROTONS:';
  WRITE 6 'EKIN           GAMMA          ';
  WRITE 6 SF(EKIN, '(E15.7)')&SF(GAMMA, '(E15.7)');
  WRITE 6 'HZ1            HZ2            QF             QD';
  WRITE 6 SF(HZ1, '(E15.7)')&SF(HZ2, '(E15.7)')&SF(QF, '(E15.7)')&SF(QD, '(E15.7)');
  WRITE 6 '+++++++++++++++++++++++++++++++++++';
ENDPROCEDURE;

PROCEDURE SET_FOR_DEUTERONS HZ1 HZ2 QF QD;
  VARIABLE EKIN 1; VARIABLE GAMMA 1; VARIABLE RESTMASSAMU 1;
  GAMMA := 1.14;
  RESTMASSAMU := 2.01410; EKIN := (GAMMA-1)*RESTMASSAMU*AMUMEV;
  RP EKIN RESTMASSAMU 1;
  RPS 1 -.142987;
  HZ1 := 0.4363977016105673; {optimized value gets FS for 3e6 turns} {SNAKE_STR(G0);}
  HZ2 := HZ1;
  QF  := -0.044;
  QD  := -0.032;
  WRITE 6 '++++++++++ SETTING UP FOR DEUTERONS:';
  WRITE 6 'EKIN           GAMMA          ';
  WRITE 6 SF(EKIN, '(E15.7)')&SF(GAMMA, '(E15.7)');
  WRITE 6 'HZ1            HZ2            QF             QD';
  WRITE 6 SF(HZ1, '(E15.7)')&SF(HZ2, '(E15.7)')&SF(QF, '(E15.7)')&SF(QD, '(E15.7)');
  WRITE 6 '+++++++++++++++++++++++++++++++++++';
ENDPROCEDURE;

PROCEDURE NAVIGATORS NU PSI DET KZ1 KZ2;
  VARIABLE LZ 1 2; VARIABLE PHIZ 1 2;
  VARIABLE ALPHA 1; VARIABLE PHIX 1;
  VARIABLE GAMMA 1; VARIABLE PSI0 1;
  GAMMA := CONS(1 + E0/(M0*AMUMEV));
  LZ (1) := .7; LZ(2) := .4;
  ALPHA := 0.039984488639998664; {value taken from SBEND in setups/BENDS24/FULL.fox}
  PHIX := GAMMA*G0*ALPHA;
  PSI0 := DEG2RAD(PSI); {BY DEFAULT DET='MPD'}
  WRITE 6 '+++++++ DETECTOR: '&DET;
  IF DET#'MPD'; {IF DET='SPD'}
    PSI0 := GAMMA*G0*PI-PSI0;
  ENDIF;
  PHIZ(1) := PI*NU*COS(PSI0);
  PHIZ(2) := PI*NU*SIN(PSI0)/SIN(PHIX);
  KZ1 := PHIZ(1)/LZ(1)/(1+G0);
  KZ2 := PHIZ(2)/LZ(2)/(1+G0);
ENDPROCEDURE;

SAVE '~/REPOS/NICA-FS/bin/common/BENDS24/header';
