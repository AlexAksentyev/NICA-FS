INCLUDE 'bin/elements';

FUNCTION ACCLEN DUMMY;
  ACCLEN := 503;
ENDFUNCTION;

FUNCTION SNAKE_STR G L; {L here is either the SPD or MPD detector length; resp. 0.7 or 5 meters}
  SNAKE_STR := PI/(1 + G)*1/L;
ENDFUNCTION;

PROCEDURE SET_FOR_PROTONS HzSPD HzMPD QF QD;
  VARIABLE EKIN 1; VARIABLE GAMMA 1; VARIABLE RESTMASSAMU 1;
  GAMMA := 1.14;
  RESTMASSAMU := 1.00728; EKIN := (GAMMA-1)*RESTMASSAMU*AMUMEV;
  RP EKIN RESTMASSAMU 1;
  RPS 1 1.792847;
  HzSPD := SNAKE_STR(G0, .7);
  HzMPD := SNAKE_STR(G0, 5);
  QF  := -0.018;
  QD  := -0.021;
  WRITE 6 '++++++++++ SETTING UP FOR PROTONS:';
  WRITE 6 'EKIN           GAMMA          ';
  WRITE 6 SF(EKIN, '(F15.7)')&SF(GAMMA, '(F15.7)');
  WRITE 6 'HzSPD            HzMPD            QF               QD';
  WRITE 6 SF(HzSPD, '(F15.7)')&SF(HzMPD, '(F15.7)')&SF(QF, '(F15.7)')&SF(QD, '(F15.7)');
  WRITE 6 '+++++++++++++++++++++++++++++++++++';
ENDPROCEDURE;

PROCEDURE NAVIGATORS NU NX NZ rdHzSPD rdHzMPD; {rdHz* = dHz*/Hz*,}
{so, this value will have to get multiplied by Hz* befire passing into LATTICE}
  VARIABLE GAMMA 1;
  GAMMA := CONS(1 + E0/(M0*AMUMEV));
  rdHzSPD := 2*NU*(NZ - NX/TAN(GAMMA*G0*PI));
  rdHzMPD := 2*NU*NX/SIN(GAMMA*G0*PI);
ENDPROCEDURE;

PROCEDURE INSERT POS MAPARR SPNRARR NFILLED;
           {inserts MAP, SPNR into lattice MAPARR, SPNRARR at POS; NFILLED elements in arrays}
  VARIABLE I 1; VARIABLE J 1; VARIABLE K 1;
  LOOP I NFILLED POS -1; WRITE 6 I;
    LOOP J 1 TWOND; MAPARR(J, I+1) := MAPARR(J, I); ENDLOOP;
    LOOP J 1 3;
      LOOP K 1 3;
        SPNRARR(J, K, I+1) := SPNRARR(J, K, I);
      ENDLOOP;
    ENDLOOP;
  ENDLOOP;
  SMAPS POS MAPARR SPNRARR;
ENDPROCEDURE;

SAVE '~/REPOS/NICA-FS/bin/common/BENDS3/header';
