INCLUDE 'bin/elements';

FUNCTION ACCLEN DUMMY;
  ACCLEN := 503;
ENDFUNCTION;

FUNCTION SNAKE_STR G L; {L here is either the SPD or MPD detector length; resp. 0.7 or 5 meters}
  SNAKE_STR := PI/(1 + G)*1/L;
ENDFUNCTION;

PROCEDURE SET_FOR_PROTONS EKIN X XM XS;  {FROM 1.4.1, p. 9}
                      {max EKIN 1.6 [GeV] (taken from p. 3)}
  VARIABLE RESTMASSAMU 1; VARIABLE GAMMA 1;
  RESTMASSAMU := 1.00728; 
  GAMMA := 1 + EKIN/(RESTMASSAMU*AMUMEV);
  RP EKIN RESTMASSAMU 1;
  RPS 1 1.792847;
  {X2}          {X3}
  X(1) := -.03; X(2) := -.005;
  {QW}          {FW}           {FE}          {QE}
  XM(1) := .02; XM(2) := -.02; XM(3) := .02; XM(4) := -.01;
  {QW}           {FW}           {FE}         {QE}
  XS(1) := -.015; XS(2) := -.02; XS(3) := 0; XS(4) := -.015;

  WRITE 6 '++++++++++ SETTING UP FOR PROTONS:';
  WRITE 6 'EKIN           GAMMA          ';
  WRITE 6 SF(EKIN, '(F15.7)')&SF(GAMMA, '(F15.7)');
  WRITE 6 '+++++++++++++++++++++++++++++++++++';
ENDPROCEDURE;

PROCEDURE NAVIGATORS NU NX NZ KS;
  VARIABLE GAMMA 1; VARIABLE PHIY 1;
  GAMMA := CONS(1 + E0/(M0*AMUMEV));
  PHIY := GAMMA*G0*PI;
  KS(1) := 2*NU*(NZ - NX/TAN(PHIY)); {SPD}
  KS(2) := 2*NU*NX/SIN(PHIY);        {MPD}
  WRITE 6 '+++++++ NAVIGATOR strength coefficients +++++++';
  WRITE 6 '        KsSPD: '&SF(KS(1), '(E15.7)');
  WRITE 6 '        KsMPS: '&SF(KS(2), '(E15.7)');
ENDPROCEDURE;

PROCEDURE INSERT POS MAPARR SPNRARR NFILLED;
           {inserts MAP, SPNR into lattice MAPARR, SPNRARR at POS; NFILLED elements in arrays}
  VARIABLE I 1; VARIABLE J 1; VARIABLE K 1;
  LOOP I NFILLED POS -1; WRITE 6 I;
    LOOP J 1 TWOND; MAPARR(J, I+1) := MAPARR(J, I); ENDLOOP;
    LOOP J 1 3;
      LOOP K 1 3;
        SPNRARR(J, K, I+1) := SPNRARR(J, K, I);
      ENDLOOP;
    ENDLOOP;
  ENDLOOP;
  SMAPS POS MAPARR SPNRARR;
ENDPROCEDURE;

SAVE '~/REPOS/NICA-FS/bin/common/FIRST-ST/header';
