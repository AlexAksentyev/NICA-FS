
INCLUDE '~/REPOS/COSYINF-CORE/bin/utilities';

PROCEDURE ROTATE PHI;
  VARIABLE M NM1 4; VARIABLE ANG NM2;
  VARIABLE i 1;
  ANG := PHI*DEGRAD;
  M(1):= COS(ANG)*MAP(1) + SIN(ANG)*MAP(3);
  M(3):=-SIN(ANG)*MAP(1) + COS(ANG)*MAP(3);
  M(2):= COS(ANG)*MAP(2) + SIN(ANG)*MAP(4);
  M(4):=-SIN(ANG)*MAP(2) + COS(ANG)*MAP(4);
  LOOP i 1 4; MAP(i):=M(i);  ENDLOOP;
  RSA PHI;
ENDPROCEDURE; {ROTATE}

PROCEDURE QUAD L {m} K1 {m^-2} ;
  {L quadrupole length}
  {K1 = B_Y/X / (B_0 R) [m^-2]}
      {COSY wants magnetic induction at the pole,}
      {which is B_y(a) = B*\rho*K_1*a, a = aperture}
      {checked with Optim; there K1 is Tl/m (not normalized by B\rho)}
      {hence B_y(a) = K1 * a in the automatic converter}
  VARIABLE Bpt 1; {magnetic induction (flux density @ pole tip)}
  VARIABLE A 1; {aperture}

  A:=1;
  { WRITE 6 'CHIM = ' CHIM; }
  
  Bpt := K1*CONS(CHIM)*A; {in Tesla}
  
  MQ L Bpt A;
ENDPROCEDURE; {QUAD}

PROCEDURE SBEND L {m} ANGLE {rad} TILT {rad};
  VARIABLE R 1; VARIABLE ANGLE0 1; VARIABLE TILT0 1;
  ANGLE0 := ANGLE/DEGRAD;
  TILT0 := TILT/DEGRAD;
  R := L/ANGLE;
  ROTATE TILT0;
    DI R ANGLE0 .1 0 0 0 0;
  ROTATE -TILT0;
ENDPROCEDURE; {SBEND}

PROCEDURE RBEND L {m} ANGLE {rad} TILT {rad};
  VARIABLE R 1; VARIABLE ANGLE0 1; VARIABLE TILT0 1;
    {all this is unneeded b/c I now just rotate the SPNR matrix}
    {together with the orbital transfer map}
    {by TILT}
    { VARIABLE GAMMA NM1; VARIABLE PHIS NM1; }
    { GAMMA := 1 + E0/(M0*AMUMEV); }
    { PHIS := GAMMA*G0*ANGLE; WRITE 6 'PHI SPIN' PHIS; }
  ANGLE0 := ANGLE/DEGRAD;
  TILT0 := TILT/DEGRAD;
  R := L/ANGLE;
  ROTATE TILT0;
    DP R ANGLE0 .1;
  ROTATE -TILT0;
ENDPROCEDURE; {RBEND}

PROCEDURE SOLENOID L {m} KS {rad/m};
  VARIABLE B0 1; VARIABLE A 1;
  A := .05;
  B0 := KS * CONS(CHIM);
  CMS B0 A L;
ENDPROCEDURE; {SOLENOID}
 
SAVE 'bin/elements';
