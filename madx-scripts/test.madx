! #################### definitions #################### 
snake_str(G, ss): MACRO = {ss = 1/12 * PI/(1 + G)*1/.7;}; //формулa (1) в файле "Пояснения к структуре"

inject_protons(gamma_): MACRO = {
		   proton_G = 1.792847;
		   EXEC, snake_str(proton_G, HZ1);
		   HZ2 := HZ1;
		   QF = -0.011;
		   QD = -0.012;
		   beam, PARTICLE=PROTON, GAMMA=gamma_;
};

inject_deuterons(gamma_): MACRO = {
		     deuteron_G = -.142987;
		     EXEC, snake_str(deuteron_G, HZ1);
		     HZ2 := HZ1;
		     QF = -.044;
		     QD = -.032;
		     beam, PARTICLE=ION, MASS=PROTON->MASS*2, GAMMA=gamma_;
};
! ##################################################

! ********** TEST PROPER **********
EXEC, inject_protons(1.14);

! testing different solenoid strength values
koef = 1; ! attenuation coefficient
HZ1 = HZ1/koef;
QF = QF/koef;
QD = QD/koef;

call file = "nica_24sol_rbend.seq";
use, sequence=NICA;

SELECT, FLAG=TWISS, COLUMN= _name, q1, q2, betx, bety, orbit5;

twiss, save, centre, file=tbl/twiss.out; 

plot, haxis=s, vaxis=betx,bety,dx,colour=100, style=100,file=img/twiss_plot;
survey, file=tbl/survey.out;

TRACK, file=tbl/basis, onetable, dump;
START, x=1e-3;
START, x=.8e-3;
START, y=1e-3;
START, y=.8e-3;
DYNAP, TURNS=1000, FASTUNE=TRUE;
RUN, turns=1000, ffile=1;
ENDTRACK;

PLOT, file=img/track_plot, table=trackone, trackfile=tbl/basis, multiple,
      haxis=turn, vaxis=x, particle=1,2,3,4;

PLOT, file=img/track_plot, table=trackone, trackfile=tbl/basis, multiple,
      haxis=x, vaxis=px, particle=1,2,3,4;

PLOT, file=img/track_plot, table=trackone, trackfile=tbl/basis, multiple,
      haxis=turn, vaxis=y, particle=1,2,3,4;

PLOT, file=img/track_plot, table=trackone, trackfile=tbl/basis, multiple,
      haxis=y, vaxis=py, particle=1,2,3,4;



VALUE, TABLE(dynaptune, tunx);
VALUE, TABLE(dynaptune, tuny);
VALUE, TABLE(summ, q1);
VALUE, TABLE(summ, q2);
VALUE, Solen_26->KS;
VALUE, Solen_191->KS;
VALUE, Quadr_36->K1;
stop;

